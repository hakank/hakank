/* 

  Self-referring sentences puzzle in Picat.

  From Adrian Groza "Modelling Puzzles in First Order Logic"
  See https://www.researchgate.net/publication/374588335_Measuring_reasoning_capabilities_of_ChatGPT
  """
  Puzzle 135. Self-referring sentences

  Which answer in the list is the correct answer to this question?
  1. All of the below.
  2. None of the below.
  3. All of the above.
  4. One of the above.
  5. None of the above.
  6. None of the above.
  """

  Solution: [0,0,0,0,1,0]
  Statement 5 is true.

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go =>
  S = new_list(6),
  S :: 0..1,
  S = [S1,S2,S3,S4,S5,S6],

  S1 #<=> (S2 #/\ S3 #/\ S4 #/\ S5 #/\ S6),
  S2 #<=> (S3 #= 0 #/\ S4 #= 0 #/\ S5 #= 0 #/\ S6 #= 0),
  S3 #<=> (S1 #/\ S2),
  S4 #<=> (S1 #\/ S2 #\/ S3),
  S5 #<=> (S1 #= 0 #/\ S2 #= 0 #/\ S3 #= 0 #/\ S4 #= 0),
  S6 #<=> (S1 #= 0 #/\ S2 #= 0 #/\ S3 #= 0 #/\ S4 #= 0 #/\ S5 #= 0),
  
  solve(S),
  println(S),
  fail,

  nl.
