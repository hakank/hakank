/* 

  How many passengers are in carriage 9 puzzle in Picat.

  MindYourDecisions:
  """
  A train has 11 passenger carriages. In any three consecutive passenger carriages, there are 
  exactly 99 passengers. If there are 381 passengers in total, how many passengers 
  are in carriage 9?
  """

  See the video https://www.youtube.com/watch?v=-rbozrj9poo

  Carriage 9 has 15 passengers. As does carriages 3 and 6.
  All other carriages has 1..83 possible number of passengers.

  This model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go ?=>
  Map = get_global_map(),
  N = 11,
  X = new_list(N),
  X :: 1..99,

  sum(X) #= 381,
  foreach(I in 1..N-2)
    sum([X[J] : J in I..I+2]) #= 99
  end,

  solve(X),
  println(X=carriage9=X[9]),
  foreach(I in 1..N)
    Map.put(I,Map.get(I,[])++[X[I]])
  end,
  fail,

  nl.
go =>
  N = 11,
  Map = get_global_map(),
  foreach(I in 1..N)
    L = Map.get(I).remove_dups,
    println(I=L=L.len)
  end,
  nl.
