/* 

  A Digial difficulty puzzle in Picat.

  Puzzle #118 from Dudeney "536 Puzzles and curious problems":
  """
  Arrange the ten digits, 1 2 3 4 5 6 7 8 9 0, in such order that they 
  shall form a number that may be divided by every number from 2 to 18 
  without in any case a remainder. As an example, if I arrange them thus, 
  1,274,953,680, this number can be divided by 2, 3, 4, 5, and so on up 
  to 16, without any remainder, but it breaks down at 17.
  """

  There are 4 solutions:

    4753869120
    4876391520
    3785942160
    2438195760


  This model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go ?=>
  N = 10,

  X = new_list(N),
  X :: 0..9,

  Num :: 10**9..10**10-1,

  X[1] #!= 0,
  foreach(I in 2..18)
    Num mod I #= 0
  end,
  
  to_num(X,10,Num),
  all_different(X),
  
  solve(X),  
  
  println(Num),
  % fail,

  nl.
go => true.


%
% converts a number Num to/from a list of integer List given a base Base
%
to_num(List, Base, Num) =>
   Len = length(List),
   Num #= sum([List[I]*Base**(Len-I) : I in 1..Len]).

