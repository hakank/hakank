/* 

  Knight and knaves: Either I am a knave or b is a knight in Picat.

  From Adrian Groza "Modelling Puzzles in First Order Logic"
  """
  Puzzle 63. Either I am a knave or b is a knight

  On the island of knights and knaves, knights always tell the truth, while knaves always
  lie. You are approached by two people. The first one says: "Either I am a knave or B is
  a knight.". What are they actually? (puzzle 29 from Smullyan (2011))
  """  

  [1,1]
  P1: Knight  P2: Knight

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go ?=>
  Knave = 0, Knight = 1,
  [P1,P2] :: [Knave,Knight],

  P1 #<=> (P1 #= Knave #\/ P2 #= Knight),

  Vars = [P1,P2],
  solve(Vars),

  println(Vars),
  Map = new_map([0="Knave",1="Knight"]),
  printf("P1: %w  P2: %w\n",Map.get(P1), Map.get(P2)),
  fail,
    
  nl.
go => true.
