/* 

  Euler #23 in Picat.

  """
  A perfect number is a number for which the sum of its proper divisors 
  is exactly equal to the number. For example, the sum of the proper divisors 
  of 28 would be 1 + 2 + 4 + 7 + 14 = 28, which means that 28 is a perfect number.

  A number n is called deficient if the sum of its proper divisors is less than 
  n and it is called abundant if this sum exceeds n.

  As 12 is the smallest abundant number, 1 + 2 + 3 + 4 + 6 = 16, the smallest number 
  that can be written as the sum of two abundant numbers is 24. By mathematical 
  analysis, it can be shown that all integers greater than 28123 can be written 
  as the sum of two abundant numbers. However, this upper limit cannot be reduced 
  any further by analysis even though it is known that the greatest number that 
  cannot be expressed as the sum of two abundant numbers is less than this limit.

  Find the sum of all the positive integers which cannot be written as the sum of 
  two abundant numbers.
  """ 

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

main => time(go).

go => euler23.

euler23 =>
  % N = 28123, 
  % From http://mathworld.wolfram.com/AbundantNumber.html: 
  %  "Every number greater than 20161 can be expressed as a sum of two abundant numbers."
  N = 20161, 
  Abundant = [A : A in 1.. N, sum_divisors3(A) > A],
  Vec = [0 : _I in 1..N].to_array(),
  foreach(A in Abundant, B in Abundant, A >= B, A+B <= N) 
    Vec[A+B] := 1
  end,
  println(sum([A : A in 1..N, Vec[A] == 0])).


euler23a =>
  % N = 28123, 
  % From http://mathworld.wolfram.com/AbundantNumber.html: 
  %  "Every number greater than 20161 can be expressed as a sum of two abundant numbers."
  N = 20161, 
  Abundant = [A : A in 1.. N, sum_divisors3(A) > A],
  Vec = new_array(N),
  foreach(I in 1..N) Vec[I] := 0 end,
  foreach(A in Abundant, B in Abundant) 
    if A >= B, A+B <= N then
      Vec[A+B] := 1
    end
  end,
  println(sum([A : A in 1..N, Vec[A] == 0])).


euler23b =>
  % N = 28123, 
  N = 20161, 
  Abundant = [A : A in 1.. N, sum_divisors3(A) > A],
  Vec = new_array(N),
  foreach(I in 1..N) Vec[I] := 0 end,
  foreach(A in Abundant, B in Abundant, A >= B, A+B <= N) 
    Vec[A+B] := 1
  end,
  println(sum([A : A in 1..N, Vec[A] == 0])).


table
sum_divisors2(N) = Sum =>
    D = floor(sqrt(N)),
    Sum1 = 1,
    foreach(I in 2..D) 
        if N mod I == 0 then
            Sum1 := Sum1+I,
            if I != N div I then
                Sum1 := Sum1 + N div I
            end
        end
    end,
    Sum = Sum1.


table
sum_divisors3(N) = Sum =>
    Sum1 = 1,
    foreach(I in 2..floor(sqrt(N)), N mod I == 0) 
        Sum1 := Sum1 + I + cond(I != N div I,N div I, 0)
    end,
    Sum = Sum1.
