/* 

  Le Monde puzzle 954 in Picat.

  http://www.r-bloggers.com/le-monde-puzzle-954/
  """
  Given a triplet (a,b,c) of integers, with a<b<c, it satisfies the S property when 
  a+b, a+c, b+c, a+b+c are perfect squares such that a+c, b+c, and a+b+c are consecutive 
  squares. For a given a, is it always possible to find a pair (b,c) such (a,b,c) 
  satisfies S? Can you find the triplet (a,b,c) that produces the sum a+b+c closest to 1000?
  """

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/


% import util.
import cp.


main => go.


go =>
  N = 1000,
  A :: 0..N,
  B :: 0..N,
  C :: 0..N,

  A #< B,
  B #< C,


  AB #= A+B,
  AC #= A+C,
  BC #= B+C,
  ABC #= A+B+C,
  perfect_square(AB,_ABsq),
  perfect_square(AC,ACsq),
  perfect_square(BC,BCsq),
  perfect_square(ABC,ABCsq),

  Z #= abs(1000-ABC),

  ACsq + 1 #= BCsq,
  BCsq + 1 #= ABCsq,


  Vars = [A,B,C,ACsq,BCsq,ABCsq],

  solve($[min(Z),ff,split],Vars),

  println([A,B,C]),
  println([ACsq,BCsq,ABCsq]),

  println(z=Z),
  nl,

  fail,
  
  nl.

perfect_square(X,V) =>
   V :: 0..X.fd_max()+1,
   V*V #= X.
