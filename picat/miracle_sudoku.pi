/* 

  Miracle Sudoku in Picat.

  From  Cracking The Cryptic "The Miracle Sudoku"
  https://www.youtube.com/watch?v=yKf9aUIxdb4&feature=emb_title&fbclid=IwAR2vYHgWUncgTpe1NjoNuAM9pv1RhTd5Dv9cLfm3snM1iGaagMrmr6M5Gc8
  """
  - Normal Sudoku rules apply. 
  - Any two cells separated by a knight's move or a king's move (in chess)
    cannot contain the same digit.
  - Any two orthogonally adjacent cells cannot contain consecutive digits.
  """

  Slate wrote about this:
  "I Really Canâ€™t Overstate How Riveting This Video of a Guy Solving a Sudoku Is - Just dynamite entertainment.":
  https://slate.com/human-interest/2020/05/the-miracle-sudoku-is-an-absolutely-riveting-video.html?via=section_features


  The Puzzle was created by Mitchell Lee.

  The problem instance is
   [[_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, 1,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  2, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _]].


  And it has actually a unique solution!

   [4,8,3, 7,2,6, 1,5,9]
   [7,2,6, 1,5,9, 4,8,3]
   [1,5,9, 4,8,3, 7,2,6]

   [8,3,7, 2,6,1, 5,9,4]
   [2,6,1, 5,9,4, 8,3,7]
   [5,9,4, 8,3,7, 2,6,1]

   [3,7,2, 6,1,5, 9,4,8]
   [6,1,5, 9,4,8, 3,7,2]
   [9,4,8, 3,7,2, 6,1,5]


 There is a lot of "symmetries" (invariants) in this solution:
  - search for the pattern <4,8,3,7> and <2,6,1,5>
   (or 
   [<4, 8, 3,7>, 2,<6, 1,5>, 9]
   [ 7,<2, 6, 1,5>, 9, 4, 8, 3]
   [ 1, 5, 9,<4, 8, 3,7>, 2, 6]
   [ 8, 3, 7,<2, 6, 1,5>, 9, 4]
   [ 2, 6, 1, 5, 9,<4, 8, 3,7>]
   [ 5, 9,<4, 8, 3, 7>,2, 6, 1]
   [ 3, 7,<2, 6, 1,5>, 9, 4, 8]
   [ 6, 1, 5, 9,<4, 8, 3,7>, 2]
   [ 9,<4, 8, 3,7>,<2, 6, 1,5>]


 We see this more clearly if we sort the lines we see that all
 the solumns are in circular rotations of 1..9,
 and the rows are rotations of 1,5,9,4,8,3,7,2,6 
 [NOTE: these are sorted lines]
   [1,5,9, 4,8,3, 7,2,6]
   [2,6,1, 5,9,4, 8,3,7]
   [3,7,2, 6,1,5, 9,4,8]
   [4,8,3, 7,2,6, 1,5,9]
   [5,9,4, 8,3,7, 2,6,1]
   [6,1,5, 9,4,8, 3,7,2]
   [7,2,6, 1,5,9, 4,8,3]
   [8,3,7, 2,6,1, 5,9,4]
   [9,4,8, 3,7,2, 6,1,5]



 Here's the solution to "A New Miracle Sudoku" created by Aad van de Wetering
 (with hints 3 and 4) is show in https://www.youtube.com/watch?v=Tv-48b-KuxI
 

  [9,4,8, 3,7,2, 6,1,5]
  [3,7,2, 6,1,5, 9,4,8]
  [6,1,5, 9,4,8, 3,7,2]

  [4,8,3, 7,2,6, 1,5,9]
  [7,2,6, 1,5,9, 4,8,3]
  [1,5,9, 4,8,3, 7,2,6]

  [8,3,7, 2,6,1, 5,9,4]
  [2,6,1, 5,9,4, 8,3,7]
  [5,9,4, 8,3,7, 2,6,1]

  Analysis: What I can see, this solution is a variant of the 
            first instance where the 9 boxes are shuffled.
  
  Here are the boxes of original problem :
    A B C 

    D E F

    G H I

  The second problem instance has the following box configuration:
   
   I G H
 
   A B C

   D E F

  For number of unique solutions, see go6/0 (and go7/0) below.


  For some more analysis of the symmetries of the 9 "base" instances found 
  in go6/0, see http://hakank.org/picat/miracle_sudoku_analyse.pi


  Also, see:
  * Solving the Miracle Sudoku in Prolog
    https://benjamincongdon.me/blog/2020/05/23/Solving-the-Miracle-Sudoku-in-Prolog/
  * Hacker News discussions on the above blog post
    https://news.ycombinator.com/item?id=23302102#23309776
    https://news.ycombinator.com/item?id=23293454



  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp,util.


main => go.

go ?=>
  problem(1,X),
  miracle_sudoku(X),
  foreach(Row in X)
    println(Row)
  end,
  nl,
  
  fail,
  nl.

go => true.

%
% A New Miracle Sudoku created by Aad van de Wetering
%
% https://www.youtube.com/watch?v=Tv-48b-KuxI
% 
go2 ?=>
  problem(2,X),
  miracle_sudoku(X),
  foreach(Row in X)
    println(Row)
  end,
  nl,
  
  fail,
  nl.

go2 => true.

%
% No hint at all: 72 solutions.
%
go3 ?=>
  Map = get_global_map(),
  Map.put(count,0),
  problem(0,X),
  miracle_sudoku(X),
  foreach(Row in X)
    println(Row)
  end,
  Map.put(count,Map.get(count)+1),
  nl,
  
  fail,
  nl.

go3 => println(num_solutions=get_global_map().get(count)).


%
% Test how many solutions there are with a 1 (or 2..9) in a single cell.
% It's always 8 solutions with no exception.
%
go4 ?=>
  Map = get_global_map(),
  Map.put(count,0),
  problem(0,X),
  foreach(T in 1..9)
    println(t=T),
    foreach(I in 1..9, J in 1..9)
      XX = copy_term(X),
      XX[I,J] #= T,
      Count= count_all(miracle_sudoku(XX)),
      if Count != 8 then
        println([t=T,i=I,j=J,count=Count])
      end
    end
  end,

  nl,
  
  fail,
  nl.

go4 => true.


%
% Place two different numbers.
% There is a lot of solutions.
% 
go5 =>
  problem(0,X),
  NumSolutions = 0,
  foreach(S in 1..9, T in 1..9, S != T)
    println([s=S,t=T]),
    foreach(I1 in 1..9, J1 in 1..9, I2 in 1..9, J2 in 1..9, (I1 != I2 ; J1 != J2))
      XX = copy_term(X),
      XX[I1,J1] #= S,
      XX[I2,J2] #= T,
      Count= count_all(miracle_sudoku(XX)),
      % println(count=Count),
      if Count == 1 then
        println([s=S,i1=I1,j1=J1,t=T,i2=I2,j2=J2,count=Count]),
        NumSolutions := NumSolutions+1
      end
    end
  end,
  println(numSolutions=NumSolutions),
  nl.

go5 => true.


%
% Check for symmetry.
% - transpose
% - rotation (1..3)
% - rotation on transpose
% It then has 9 unique solutions.
% (I might have missed some variant...)
%
% 1 (cf sol 8)
% [5,9,4,8,3,7,2,6,1]
% [8,3,7,2,6,1,5,9,4]
% [2,6,1,5,9,4,8,3,7]
% [9,4,8,3,7,2,6,1,5]
% [3,7,2,6,1,5,9,4,8]
% [6,1,5,9,4,8,3,7,2]
% [4,8,3,7,2,6,1,5,9]
% [7,2,6,1,5,9,4,8,3]
% [1,5,9,4,8,3,7,2,6]

% 2
% [3,6,9,7,1,4,2,5,8]
% [7,1,4,2,5,8,6,9,3]
% [2,5,8,6,9,3,1,4,7]
% [6,9,3,1,4,7,5,8,2]
% [1,4,7,5,8,2,9,3,6]
% [5,8,2,9,3,6,4,7,1]
% [9,3,6,4,7,1,8,2,5]
% [4,7,1,8,2,5,3,6,9]
% [8,2,5,3,6,9,7,1,4]


% 3
% [4,8,3,7,2,6,1,5,9]
% [7,2,6,1,5,9,4,8,3]
% [1,5,9,4,8,3,7,2,6]
% [8,3,7,2,6,1,5,9,4]
% [2,6,1,5,9,4,8,3,7]
% [5,9,4,8,3,7,2,6,1]
% [3,7,2,6,1,5,9,4,8]
% [6,1,5,9,4,8,3,7,2]
% [9,4,8,3,7,2,6,1,5]

% 4
% [7,1,4,2,5,8,6,9,3] cf 2: row 2
% [3,6,9,7,1,4,2,5,8] cf 2: row 1
% [8,2,5,3,6,9,7,1,4]
% [4,7,1,8,2,5,3,6,9]
% [9,3,6,4,7,1,8,2,5]
% [5,8,2,9,3,6,4,7,1]
% [1,4,7,5,8,2,9,3,6]
% [6,9,3,1,4,7,5,8,2]
% [2,5,8,6,9,3,1,4,7] 2: row 3

% 5
% [6,9,3,1,4,7,5,8,2]
% [2,5,8,6,9,3,1,4,7] 2: row 3
% [7,1,4,2,5,8,6,9,3]
% [3,6,9,7,1,4,2,5,8]
% [8,2,5,3,6,9,7,1,4]
% [4,7,1,8,2,5,3,6,9]
% [9,3,6,4,7,1,8,2,5]
% [5,8,2,9,3,6,4,7,1]
% [1,4,7,5,8,2,9,3,6]

% 6
% [4,8,3,7,2,6,1,5,9]
% [1,5,9,4,8,3,7,2,6]
% [7,2,6,1,5,9,4,8,3]
% [9,4,8,3,7,2,6,1,5]
% [6,1,5,9,4,8,3,7,2]
% [3,7,2,6,1,5,9,4,8]
% [5,9,4,8,3,7,2,6,1]
% [2,6,1,5,9,4,8,3,7]
% [8,3,7,2,6,1,5,9,4]

% 7
% [3,7,2,6,1,5,9,4,8]
% [9,4,8,3,7,2,6,1,5]
% [6,1,5,9,4,8,3,7,2]
% [8,3,7,2,6,1,5,9,4]
% [5,9,4,8,3,7,2,6,1]
% [2,6,1,5,9,4,8,3,7]
% [4,8,3,7,2,6,1,5,9]
% [1,5,9,4,8,3,7,2,6]
% [7,2,6,1,5,9,4,8,3]

% 8
% [5,9,4,8,3,7,2,6,1]  Cf 1: row 1
% [2,6,1,5,9,4,8,3,7]  Cf 1: row 3
% [8,3,7,2,6,1,5,9,4]  Cf 1: row 2
% [1,5,9,4,8,3,7,2,6]  Cf 1: row 9
% [7,2,6,1,5,9,4,8,3]  Cf 1: row 8
% [4,8,3,7,2,6,1,5,9]  Cf 1: row 7
% [6,1,5,9,4,8,3,7,2]  Cf 1: row 6
% [3,7,2,6,1,5,9,4,8]  Cf 1: row 5
% [9,4,8,3,7,2,6,1,5]  cf 1: row 4

% 9
% [2,7,3,8,4,9,5,1,6]
% [5,1,6,2,7,3,8,4,9]
% [8,4,9,5,1,6,2,7,3]
% [6,2,7,3,8,4,9,5,1]
% [9,5,1,6,2,7,3,8,4]
% [3,8,4,9,5,1,6,2,7]
% [1,6,2,7,3,8,4,9,5]
% [4,9,5,1,6,2,7,3,8]
% [7,3,8,4,9,5,1,6,2]

go6 ?=>
  problem(0,X),
  check_symmetries(X),
  nl.

go6 => true.

%
% All solutions on problem 3 (1 hint).
% Originally this has 8 solutions.
% With symmetries removed there are just 2 solutions:
%
% [2,7,3,8,4,9,5,1,6]
% [5,1,6,2,7,3,8,4,9]
% [8,4,9,5,1,6,2,7,3]
% [6,2,7,3,8,4,9,5,1]
% [9,5,1,6,2,7,3,8,4]
% [3,8,4,9,5,1,6,2,7]
% [1,6,2,7,3,8,4,9,5]
% [4,9,5,1,6,2,7,3,8]
% [7,3,8,4,9,5,1,6,2]
%
% [4,8,3,7,2,6,1,5,9]
% [7,2,6,1,5,9,4,8,3]
% [1,5,9,4,8,3,7,2,6]
% [8,3,7,2,6,1,5,9,4]
% [2,6,1,5,9,4,8,3,7]
% [5,9,4,8,3,7,2,6,1]
% [3,7,2,6,1,5,9,4,8]
% [6,1,5,9,4,8,3,7,2]
% [9,4,8,3,7,2,6,1,5]
%
go7 =>
  problem(3,X),
  check_symmetries(X),
  nl.

%
% What if we sort the lines, how many distinct solutions do we get?
%
% Note: These are not Sudokus but tells something about the structure
% of the solutions
% There are then are 4 _sorted_ matrices (all have 18 Sudoku solutions)
%
% [1,5,9,4,8,3,7,2,6]
% [2,6,1,5,9,4,8,3,7]
% [3,7,2,6,1,5,9,4,8]
% [4,8,3,7,2,6,1,5,9]
% [5,9,4,8,3,7,2,6,1]
% [6,1,5,9,4,8,3,7,2]
% [7,2,6,1,5,9,4,8,3]
% [8,3,7,2,6,1,5,9,4]
% [9,4,8,3,7,2,6,1,5]
%
% [1,4,7,5,8,2,9,3,6]
% [2,5,8,6,9,3,1,4,7]
% [3,6,9,7,1,4,2,5,8]
% [4,7,1,8,2,5,3,6,9]
% [5,8,2,9,3,6,4,7,1]
% [6,9,3,1,4,7,5,8,2]
% [7,1,4,2,5,8,6,9,3]
% [8,2,5,3,6,9,7,1,4]
% [9,3,6,4,7,1,8,2,5]
%
% [1,7,4,6,3,9,2,8,5]
% [2,8,5,7,4,1,3,9,6]
% [3,9,6,8,5,2,4,1,7]
% [4,1,7,9,6,3,5,2,8]
% [5,2,8,1,7,4,6,3,9]
% [6,3,9,2,8,5,7,4,1]
% [7,4,1,3,9,6,8,5,2]
% [8,5,2,4,1,7,9,6,3]
% [9,6,3,5,2,8,1,7,4]
%
% [1,6,2,7,3,8,4,9,5]
% [2,7,3,8,4,9,5,1,6]
% [3,8,4,9,5,1,6,2,7]
% [4,9,5,1,6,2,7,3,8]
% [5,1,6,2,7,3,8,4,9]
% [6,2,7,3,8,4,9,5,1]
% [7,3,8,4,9,5,1,6,2]
% [8,4,9,5,1,6,2,7,3]
% [9,5,1,6,2,7,3,8,4]
%
go8 =>
  problem(0,X),
  Sols = new_map(),
  % Get all solutions
  All = find_all(X,miracle_sudoku(X)),
  foreach(S in All)
    % Check if we have seen these before
    SS = sort(S),
    Sols.put(SS,Sols.get(SS,[])++[S])
  end,

  foreach(S in Sols.keys)
    foreach(Row in S) println(Row) end,
    println(num=Sols.get(S).len),
    % foreach(V in Sols.get(S))
    %   foreach(Row in V)
    %     println(Row)
    %   end,
    %   nl
    % end,
    nl
  end,
  println(numSols=Sols.keys.len),
  nl.


%
% Solve Miracle Sudoku
%
miracle_sudoku(X) =>
   N = 9,
   N3 = 3,

   Vars = X.vars(),
   Vars :: 1..N,

   % Sudoku
   foreach(Row in X)
     all_different(Row)
   end,
   foreach(Column in transpose(X))
     all_different(Column)
   end,
   
   foreach(I in 1..N3..N, J in 1..N3..N)
     all_different([X[I+K,J+L] : K in 0..N3-1, L in 0..N3-1])
   end,

   % Knight move: must be different
   foreach(I in 1..N, J in 1..N, A in -2..2, B in -2..2,
      membchk(I+A,1..N), membchk(J+B,1..N), abs(A)+abs(B)==3)
      X[I,J] #!= X[I+A,J+B]
   end,

   % Kings move: must be different
   foreach(I in 1..N, J in 1..N, A in -1..1, B in -1..1,
      membchk(I+A,1..N), membchk(J+B,1..N), (A != 0 ; B != 0))
      X[I,J] #!= X[I+A,J+B]
   end,

   % Orthogonally adjacent (i.e. same row, same column): cannot contain consecutive digits
   foreach(I in 1..N)
     foreach(J in 2..N)
       abs(X[I,J-1]-X[I,J]) #> 1,
       abs(X[J-1,I]-X[J,I]) #> 1       
     end
   end,

   % println("Presolve:"),
   % foreach(I in 1..N)
   %   foreach(J in 1..N)
   %     fd_min_max(X[I,J],Min,Max),
   %     printf("%d..%d ", Min,Max)
   %   end,
   %   nl
   % end,
   % nl,
   solve([ffd,down], Vars).

%
% Rotate a matrix (non cp form)
%
rotate_matrix(X) = Rot.array_matrix_to_list_matrix() =>
  N = X.len,
  Rot = new_array(N,N),
  foreach(I in 1..N)
    foreach(J in 1..N)
      Rot[I,J] := X[N-J+1,I]
    end
  end.

%
% Flip a matrix upside down
%
flip_matrix(X) = F =>
  N = X.len,
  F = [],
  foreach(I in N..-1..1)
    F := F ++ [[X[I,J] : J in 1..N]]
  end.

%
% Check symmetries for a Sudoku instance X
%
check_symmetries(X) =>
  Sols = new_map(),
  Sym = new_map(),
  % Get all solutions
  All = find_all(X,miracle_sudoku(X)),
  foreach(S in All)
    % Check if we have seen these before
    if not Sym.has_key(S) then
      Sols.put(S,1)
    end,
    T = S.transpose(),
    Sym.put(T,1),
    Rot1 = rotate_matrix(S),
    Rot2 = rotate_matrix(Rot1),
    Rot3 = rotate_matrix(Rot2),    
    Rot4 = rotate_matrix(T),
    Rot5 = rotate_matrix(Rot4),
    Rot6 = rotate_matrix(Rot5),    
    Flip1 = flip_matrix(S),
    Rot7 = rotate_matrix(Flip1),
    Rot8 = rotate_matrix(Rot7),
    Rot9 = rotate_matrix(Rot8),
    Flip2 = flip_matrix(T),
    Rot10 = rotate_matrix(Flip2),
    Rot11 = rotate_matrix(Rot10),
    Rot12 = rotate_matrix(Rot11),
    
    foreach(Rot in [S,T,Rot1,Rot2,Rot3,Rot4,Rot5,Rot6,
                    Flip1,Flip2,Rot7,Rot8,Rot9,Rot10,Rot11,Rot12])
      Sym.put(Rot,1)
    end
  end,

  % println(sols=Sols),
  C := 1,
  foreach(S=1 in Sols)
    printf("%d\n", C),
    C := C + 1,
    foreach(Row in S)
      println(Row)
    end,
    nl
  end,
  println(numSols=Sols.keys.len),
  nl.



% No hints
problem(0,X) =>
  X =
   [[_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _]].


problem(1,X) =>
  X =
   [[_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, 1,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  2, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _]].


%
% A New Miracle Sudoku created by Aad van de Wetering
%
% https://www.youtube.com/watch?v=Tv-48b-KuxI
% 
problem(2,X) =>
  X =
   [[_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, 4, _,  _, _, _],
    
    [_, _, 3,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _]].


%
% A single 1 as hint
%
problem(3,X) =>
  X =
   [[_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, 1,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _],
    [_, _, _,  _, _, _,  _, _, _]].

