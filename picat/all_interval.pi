/*

  All interval problem in Picat.

  CSPLib problem number 7
  http://www.cs.st-andrews.ac.uk/~ianm/CSPLib/prob/prob007/index.html
  """
  Given the twelve standard pitch-classes (c, c , d, ...), represented by 
  numbers 0,1,...,11, find a series in which each pitch-class occurs exactly 
  once and in which the musical intervals between neighbouring notes cover 
  the full set of intervals from the minor second (1 semitone) to the major 
  seventh (11 semitones). That is, for each of the intervals, there is a 
  pair of neigbhouring pitch-classes in the series, between which this 
  interval appears. The problem of finding such a series can be easily 
  formulated as an instance of a more general arithmetic problem on Z_n, 
  the set of integer residues modulo n. Given n in N, find a vector 
  s = (s_1, ..., s_n), such that (i) s is a permutation of 
  Z_n = {0,1,...,n-1}; and (ii) the interval vector 
  v = (|s_2-s_1|, |s_3-s_2|, ... |s_n-s_{n-1}|) is a permutation of 
  Z_n-{0} = {1,2,...,n-1}. A vector v satisfying these conditions is 
  called an all-interval series of size n; the problem of finding such 
  a series is the all-interval series problem of size n. We may also be 
  interested in finding all possible series of a given size. 
  """

  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/
import cp.

main => time2(go).

go =>
        N = 12,
        L = findall(X,all_interval(N,X,[])),
        Len = length(L),
        printf("Total: %d\n", Len).

% Just the first solution
go2 =>
        foreach(N in 2..220) 
           println(n=N),
           time2($all_interval(N,_X,[ff,split]))
        end,
        nl.

go3 => 
   all_interval(2000,X,[ff,split]),
   println(X),
   nl.


% Number of solutions
go4 =>
   foreach(N in 2..30) 
      % println(n=N),
      time2(C = count_all(all_interval(N,_X,[],false))),
      println(N=C)
   end,
   nl.

all_interval(N,X,Label) =>
   all_interval(N,X,Label,true).
all_interval(N,X,Label,Print) =>

        X = new_list(N),
        X :: 1..N,

        N1 = N-1,
        Diffs = new_list(N1),
        Diffs :: 1..N1,

        SumDistinct = ((N+1)*N) // 2,

        all_different(X),
        all_different(Diffs),
        
        foreach(K in 1..N1)
           Diffs[K] #= abs(X[K+1] - X[K])
        end,
        
        % symmetry breaking
        X[1] #< X[N1],
        Diffs[1] #< Diffs[2],

        Vars = X ++ Diffs,

        % symmetry breaking for one solutions
        % increasing(Diffs),

        %
        % This first solution is found very fast using [ff,split],
        % then the Diffs is the sequence 1..N-1.
        % For getting all solution, the default labeling is better.
        %
        % solve([ff,down], Vars),
        % solve([ff,split], Vars),
        solve(Label, Vars),
        % solve(Vars),
        if Print then
          printf("x: %w  diffs: %w  [sum: %d]\n", X, Diffs, SumDistinct)
        end.
