/* 

  Caesar cipher (Rosetta code) in Picat.

  http://rosettacode.org/wiki/Caesar_cipher
  """
  Implement a Caesar cipher, both encoding and decoding. The key is an integer from 1 to 25. 
  This cipher rotates (either towards left or right) the letters of the alphabet (A to Z). 
  The encoding replaces each letter with the 1st to 25th next letter in the alphabet 
  (wrapping Z to A). So key 2 encrypts "HI" to "JK", but key 20 encrypts "HI" to "BC". 
  This simple "monoalphabetic substitution cipher" provides almost no security, because an 
  attacker who has the encoded message can either use frequency analysis to guess the key, 
  or just try all 25 keys.

  Caesar cipher is identical to VigenÃ¨re cipher with key of length 1. Also, Rot-13 is 
  identical to Caesar cipher with key 13. 
  """

  * go/0: The Rosetta code assignment
  * go2/0: Cracking a cipher with a wordlist
  * go3/0: Rot13


  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import os. % for file/1
import util. % for split/2
import cp.
import v3_utils.

main => go.

% 
% Using command line.
%
% * Arguments: string number
%   $ picat caesar_cipher.pi "Wkh ilyh eralqj zlcdugv mxps txlfnob" 23
%   s = Wkh ilyh eralqj zlcdugv mxps txlfnob
%   n = 23
%   The five boxing wizards jump quickly
%
% * Arguments: string wordlist
%   $ picat caesar_cipher.pi "Wkh ilyh eralqj zlcdugv mxps txlfnob" /usr/share/dict/words
%   ...
%   n=20 c=Qeb cfsb ylufkd tfwxoap grjm nrfzhiv found= 0
%   n=21 c=Rfc dgtc zmvgle ugxypbq hskn osgaijw found= 0
%   n=22 c=Sgd ehud anwhmf vhyzqcr itlo pthbjkx found= 0
%   n=23 c=The five boxing wizards jump quickly found= 6
%   Best so far!
%
%   n=24 c=Uif gjwf cpyjoh xjabset kvnq rvjdlmz found= 0
%   n=25 c=Vjg hkxg dqzkpi ykbctfu lwor swkemna found= 0
%   n = 23
%   string = The five boxing wizards jump quickly
%   numWordMatched = 6
%
% * Arguments: just a string => brute force
%   $ picat caesar_cipher.pi "Wkh ilyh eralqj zlcdugv mxps txlfnob"
%   ...
%   17 = Nby zcpy vircha qctulxm dogj kocwefs
%   18 = Ocz adqz wjsdib rduvmyn ephk lpdxfgt
%   19 = Pda bera xktejc sevwnzo fqil mqeyghu
%   20 = Qeb cfsb ylufkd tfwxoap grjm nrfzhiv
%   21 = Rfc dgtc zmvgle ugxypbq hskn osgaijw
%   22 = Sgd ehud anwhmf vhyzqcr itlo pthbjkx
%   23 = The five boxing wizards jump quickly
%   24 = Uif gjwf cpyjoh xjabset kvnq rvjdlmz
%   25 = Vjg hkxg dqzkpi ykbctfu lwor swkemna
% 
main(ARGS) =>
  if ARGS.len == 2 then
    if file(ARGS[2]) then
      % Crack using a wordlist
      S = ARGS[1],
      File = ARGS[2],
      [N,String,NumWordMatched] = crack_ceasar(S,File),
      println(n=N),
      println(string=String),
      println(numWordMatched=NumWordMatched),
      nl
    else
      % Shift is given
      S = ARGS[1],
      N = ARGS[2].to_int,
      println(s=S),
      println(n=N),
      println(caesar(S,N)),
      nl
    end
  else
    % Brute force (no wordlist)
    S = ARGS[1],    
    foreach(N in 1..25)
      println(N=caesar(S,N))
    end
  end,
  
  nl.

go =>
  S = "All human beings are born free and equal in dignity and rights.",
  println(S),
  println(caesar(S,5)),
  println(caesar(caesar(S,5),-5)),

  S2 = "IBM",
  println(S2),
  println(caesar(S2,25)), % -> HAL
  println(caesar(S2,-1)), % -> HAL

  nl.

%
% Cracking a cipher
%
go2 =>
  S = "Vgg cphvi wzdibn vmz wjmi amzz viy zlpvg di ydbidot viy mdbcon.",
  File = "unixdict.txt",
  % File = "words_lower.txt",
  [N,String,NumWordMatched] = crack_ceasar(S,File),
  println(n=N),
  println(string=String),
  println(numMatchedWords=NumWordMatched),
  nl.

%
% Rot-13
%
go3 =>
  S = "Big fjords vex quick waltz nymph!",
  println(caesar(S,13)),
  println(caesar(caesar(S,13),13)),
  nl.

lower(Lower) => Lower = "abcdefghijklmnopqrstuvwxyz".
upper(Upper) => Upper = "ABCDEFGHIJKLMNOPQRSTUVWXYZ".

caesar(String, N) = Cipher =>
  lower(Lower),upper(Upper),
  M = create_map(Lower, Upper, N),
  % If a char is not in a..zA..z then show it as it is.
  Cipher := [M.get(C,C) : C in String].

%
% create the map
%
create_map(Lower,Upper, N) = M =>
  M = new_map(),
  Len = Lower.length,
  foreach(I in 1..Len)
    II = (N+I) mod Len,
    if II == 0 then II := Len end, % Adjust for 1 based
    M.put(Upper[I],Upper[II]),
    M.put(Lower[I],Lower[II])
  end.


%
% Cracking a Caesar cipher using a dictionary.
%
crack_ceasar(S,File) = [MaxN,MaxString,MaxFound] =>
  Map = new_map([W=1 : W in read_file_lines(File)]),
  MaxN = 0,
  MaxFound = 0,
  MaxString = "",
  foreach(N in 1..25)
    C = caesar(S,N),
    Found = 0,
    foreach(W in C.split(" ")) 
      if Map.has_key(W.to_lowercase()) then
        Found := Found + 1
      end
    end,
    printf("n=%2d c=%w found=%2d\n", N, C, Found),
    if Found > 0, Found > MaxFound then
      MaxN := N,
      MaxString := C,
      MaxFound := Found,
      println("Best so far!\n")
    end
  end.

