/*

  Tourist Site Competition in Picat.

  From Pierre Flener's presentation 
  "Constraint Technology - A Programming Paradigm on the Rise"
  http://www.it.uu.se/edu/course/homepage/ai/vt08/AI-CT.pdf
     pages 5f: problem statement 
     pages 12f: model
     pages 21ff: walktrough of a solution

  With 7 tourist sites and 7 judges:
  """
  Every tourist site is visited by r = 3 judges.
  Every judge visits c = 3 tourist sites.
  Every pair of sites is visited by lambda = 1 common judge.
  """

  There are 151200 solutions to this problem.
  With the additional constraint that Ali should visit Birka, Falun and Lund
  there are 4320 solutions.


  This problem was also presented as "The Airline-of-the-Year Problem"
  in his (Flener's) presentation
  "Constraint Programming - Programming Paradigm on the Rise"
  http://www.it.uu.se/research/group/astra/ATM-CT/Flener.pdf
  page 4f
  The problem is stated as follows for 7 airlines and 7 judges:
  """
  Constant jury: Every airline is tested by 3 judges.
  Constant load: Every judge tests 3 airlines.
  Equity: Every airline pair is tested by 1 common judge.
  """

  Model created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.
import cp.

main => go.

go =>
   R = 3,
   C = 3,
   Lambda = 1,

   % Sites
   Birka = 1,
   Falun = 2,
   Lund = 3,
   Mora = 4,
   Sigtuna = 5,
   Uppsala = 6,
   Ystad = 7,
   Sites = [Birka, Falun, Lund, Mora, Sigtuna, Uppsala, Ystad],
   SitesStr = ["Birka", "Falun", "Lund", "Mora", "Sigtuna", "Uppsala", "Ystad"],
   % Judges
   Ali = 1,
   Dan = 2,
   Eva = 3,
   Jim = 4,
   Leo = 5,
   Mia = 6,
   Ulla = 7,
   Judges = [Ali, Dan, Eva, Jim, Leo, Mia, Ulla],
   JudgesStr = ["Ali", "Dan", "Eva", "Jim", "Leo", "Mia", "Ulla"],
   
   SymmetryBreaking = true,
   tourist_site_competition(Sites,Judges,R,C, Lambda,SymmetryBreaking,X),
   foreach(Row in X) writeln(Row) end,
   nl,
   print_assignments(X,SitesStr,JudgesStr).


% Checking the number of solutions
go2 =>
   NumJudges = 7,
   Judges = 1..NumJudges,
   NumSites = 7,
   Sites = 1..NumSites,
   R = 3,
   C = 3,
   Lambda = 1,
   printf("It is %d solutions.\n", num_solutions(Sites,Judges,R,C,Lambda)),
   nl.


num_solutions(Sites,Judges,R,C,Lambda) = NumSolutions =>
  L = findall(X, $tourist_site_competition(Sites,Judges,R,C, Lambda,_,X)),
  NumSolutions = L.length.

tourist_site_competition(Sites,Judges,R,C,Lambda,SymmetryBreaking,X) =>

   NumSites = length(Sites),
   NumJudges = length(Judges),
   writeln([numSites=NumSites, numJudges=NumJudges]),

   % decision variable
   X = new_array(NumSites,NumJudges),
   Vars = vars(X),
   Vars :: 0..1,

   % Symmetry breaking: Assigns the first site to judges {1,2,3}
   if SymmetryBreaking = true then
       foreach(I1 in 1..1..R) X[I1,1] #= 1 end
   end,

   % Every tourist site is visited by R judges.
   foreach(Row in X) sum(Row.to_list()) #= R end,

   % Every judge visits C tourist sites.
   foreach(Column in X.columns().array_matrix_to_list_matrix()) sum(Column) #=C end,

   % Every pair of sites is visited by Lambda common judges.
   % Rows = X.to_list(),
   foreach({X1,I} in zip(X.to_list(), 1..X.length))
      foreach({X2,J} in zip(X.to_list(),1..NumSites))
         if I < J then
           sum([ (XX1 #= 1 #/\ XX1 #= XX2) :
                {XX1,XX2} in zip(X1.to_list(), X2.to_list()) ]) #>= Lambda
         end
      end
   end,
   
   % search
   solve([ff], Vars).



print_assignments(X,SitesStr,JudgesStr) =>
   Len = X.length,
   foreach({Row,S} in zip(X.to_list(),SitesStr))
          printf("%w\t: ",S),
          Where = [ Judge : {R,I} in zip(Row.to_list(), 1..Len), R == 1,  nth(I,JudgesStr,Judge)],
          printf("%w\n", Where)
   end,
   nl.


