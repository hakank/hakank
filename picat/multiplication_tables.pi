/* 

  Multiplication tables (Rosetta code) in Picat.

  http://rosettacode.org/wiki/Multiplication_tables

  This Picat model was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/


import util.

main => go.

% as a "one-liner"
go =>
  N = 12, 
  ((" " ++[ to_fstring("%3w", I) : I in 1..N].join(' ')) ++ "\n" ++
  ['-' : _ in 1..(N+1)*4] ++ "\n" ++
  [ [to_fstring("%4w",cond(J>=I,I*J,'')) : J in 1..N ].join('') ++ " |" ++ to_fstring("%2d",I) :I in 1..N].join("\n")).println().

go2 => 
  N=12,
  foreach(I in 1..N) printf("%4w", I) end,
  nl,
  println(['-' : _ in 1..(N+1)*4]),
  foreach(I in 1..N)
    foreach(J in 1..N)
      if J>=I then
        printf("%4w", I*J)
      else
        printf("    ")
      end
    end,
    printf(" |%2d\n", I)
  end,
  nl.

