/* 

  Lying about your age in Picat.

  From https://github.com/the-faisalahmed/Optimization/blob/main/logic%20puzzles/Lying_about_your_Age.ipynb
  """
  Annie, Betty, Carrie, Darla, and Eve recently found out that all of their birthdays 
  were on the same day, though they are different ages. On their mutual birthday, they 
  were jabbering away, flapping their gums about their recent discovery. And, lucky me, 
  I was there. Some of the things that I overheard were...

  1. Darla said to Betty: "I'm nine years older than Eve."
  2. Eve said to Betty: "I'm seven years older than Annie."
  3. Annie said to Betty: "Your age is exactly 70% greater than mine."
  4. Betty said to Carrie: "Eve is younger than you."
  5. Carrie said to Darla: "The difference between our ages is six years."
  6. Carrie said to Annie: "I'm ten years older than you."
  7. Carrie said to Annie: "Betty is younger than Darla."
  8. Betty said to Carrie: "The difference between your age and Darla's is the same 
                            as the difference between Darla's and Eve's."

  Since I knew these people -- and how old they were, I knew that they were not telling 
  the whole truth. After thinking about it, I realized that when one of them spoke to 
  somebody older than herself, everything she said was true, but when speaking to somebody 
  younger, everything she said was false.

  How old is each person?
  """

  (The source of this puzzle seems to be 
   https://en.wikibooks.org/wiki/Puzzles/Logic_puzzles/Lying_about_your_Age )


  Solution:
  Annie  : 30
  Betty  : 51
  Carrie : 55
  Darla  : 46
  Eve    : 37


  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import sat.

main => go.

go ?=>
  nolog,
  Age = [Annie, Betty, Carrie, Darla, Eve],
  Age :: 1..100,

  Names = ["Annie", "Betty", "Carrie", "Darla", "Eve"],

  all_different(Age),

  % [W]hen one of them spoke to somebody older than herself, everything she said was
  % true, but when speaking to somebody younger, everything she said was false.

  % 1. Darla said to Betty: "I'm nine years older than Eve."
  Darla #< Betty #<=> Darla #= Eve + 9,
  
  % 2. Eve said to Betty: "I'm seven years older than Annie."
  Eve #< Betty #<=> Eve #= Annie + 7,
  
  % 3. Annie said to Betty: "Your age is exactly 70% greater than mine."
  Annie #< Betty #<=>  Betty * 10 #= Annie * 17,

  % 4. Betty said to Carrie: "Eve is younger than you."
  Betty #< Carrie #<=> Eve #< Carrie,

  % 5. Carrie said to Darla: "The difference between our ages is six years."
  Carrie #< Darla #<=> abs(Carrie-Darla) #= 6,

  % 6. Carrie said to Annie: "I'm ten years older than you."
  Carrie #< Annie #<=> Carrie #= Annie + 10,

  % 7. Carrie said to Annie: "Betty is younger than Darla."
  Carrie #< Annie #<=> Betty #< Darla,

  % 8. Betty said to Carrie: "The difference between your age and Darla's is the same 
  %                           as the difference between Darla's and Eve's."
  Betty #< Carrie #<=> abs(Carrie-Darla) #= (abs(Darla-Eve)),


  solve(Age),
  println(Age),
  foreach(I in 1..5)
    printf("%-7w: %2d\n",Names[I],Age[I])
  end,
  nl,
  fail,
  
  nl.
go => true.
