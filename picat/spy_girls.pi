/* 

  Spy girls puzzle in Picat.

  From
  http://cs.calstatela.edu/wiki/index.php/Courses/CS_460/Fall_2006/Nadeem,_Hassan/Spy_Girls
  """
                                         SPY GIRLS
  The Spy Girls Agency, the all-female spy agency, consists of Jane and 
  four other agents, one of whom is Agent Brookes. Each agent is ranked 
  by her secret agent number, from Agent 1 (highest) to Agent 5 (lowest). 
  From this information and the following clues, determine each agent's rank, 
  first name, and last name.
  
   1. Sylvia is ranked higher than Agent Adams, but is not the highest.
   2. Hillary's rank is higher than Agent Darling, but not as high as 
      Agent Brookes.
   3. Amanda is ranked higher than either Kate or Hillary, but is not the 
      highest.
   4. Agent Adams is ranked higher than Agent Miller, but is ranked lower 
      than Agent Cooper.
   5. Agent Miller is not ranked the lowest.
  """

  Answer:

  first_names = [1, 2, 4, 5, 3]
  last_names =  [2, 3, 5, 1, 4]


  Rank  Name
  1     Kate Cooper
  2     Jane Brookes
  3     Amanda Adams
  4     Hillary Miller
  5     Kate Darling


  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

go =>
  N = 5,
  R = 1..N,
  FirstNames = [Jane, Sylvia, Hillary, Kate, Amanda],
  FirstNames :: R,
  FirstNamesS = ['Jane', 'Sylvia', 'Hillary', 'Kate', 'Amanda'],  

  LastNames = [Brookes, Adams, Darling, Cooper, Miller],
  LastNames :: R,
  LastNamesS = ['Brookes', 'Adams', 'Darling', 'Cooper', 'Miller'],  

  all_different(FirstNames),
  all_different(LastNames),

  % 1 is highest rank, 5 is lowest

  % The Spy Girls Agency, the all-female spy agency, consists of Jane and 
  % four other agents, one of whom is Agent Brookes. 
  Jane #!= Brookes,
 
  %  1. Sylvia is ranked higher than Agent Adams, but is not the highest.
  Sylvia #< Adams,
  Sylvia #> 1,

  %  2. Hillary's rank is higher than Agent Darling, but not as high as 
  %     Agent Brookes.
  Hillary #< Darling,
  Brookes #< Hillary,


  %  3. Amanda is ranked higher than either Kate or Hillary, but is not the 
  %     highest.
  Amanda #< Kate,
  Amanda #< Hillary,
  Amanda #> 1,

  %  4. Agent Adams is ranked higher than Agent Miller, but is ranked lower 
  %     than Agent Cooper. 
  Adams #< Miller,
  Adams #> Cooper,
  
  %  5. Agent Miller is not ranked the lowest.
  Miller #< 5,


  Vars = FirstNames ++ LastNames,
  solve(Vars),

  println(FirstNames),
  println(LastNames),
  foreach(I in R)
    nth(FN,FirstNames,I),
    nth(LN,LastNames,I),
    printf("Rank %d: %w %w\n", I,FirstNamesS[FN],LastNamesS[LN])
  end,
  nl,
  fail,
 
  nl.
