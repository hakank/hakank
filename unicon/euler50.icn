#
# Euler problem 50
#
# """
# The prime 41, can be written as the sum of six consecutive primes:
# 41 = 2 + 3 + 5 + 7 + 11 + 13
#
# This is the longest sum of consecutive primes that adds to 
# a prime below one-hundred.
#
# The longest sum of consecutive primes below one-thousand that 
# adds to a prime, contains 21 terms, and is equal to 953.
#
# Which prime, below one-million, can be written as the sum of the 
# most consecutive primes?
#
# """
#
# This program was written by Hakan Kjellerstrand (hakank@gmail.com)
# Also see my Icon/Unicon page http://www.hakank.org/icon/
#

link numbers

procedure main()
   problem50() # 0.736s
end

procedure problem50()
   local n,primes,found

   n := 1000000

   # Generate the primes: total 2.7s
   # p := 2;
   # primes := [p];
   # while p := nxtprime(p) & p < n do {
   #     put(primes, p)
   # }

   # Faster
   primes := sort(sieve(n))

   
   ## Using the prime() generator: Much slower: total 4.3s
   # primes := []
   # every p := prime() do {
   #     if p > n then break
   #     put(primes,p)
   # }
   # Variant, still slow:
   # primes := [: p := prime() & if p < n then p else break :] 

   # Check for the solution
   found := 0
   pp := 0
   every len := 550 to 21 by -1 do {
       if found = 1 then 
           break
       every offset := 1 to 549 do {
           pp := 0
           every j := offset+1 to offset+len do {
              pp +:= primes[j]
           }
           if pp < 1000000 & isprime(pp) then {
               found := 1
               break 0
           }
       }
   }
   write(pp)
end

##
## Memoized variant of isprime(n)
## Note: it don't memoize non-primes (instead it fail)
## 
procedure m_isprime(n)
    static primes := set()
    member(primes, n) & return n 
    if isprime(n) then {
        return insert(primes, n)
    } else {
        fail
    }

end


procedure sieve(n)
   local s, i
   s := set()
   every insert(s,1 to n)
   every member(s,i := 2 to n) do
      every delete(s,i + i to n by i)
   delete(s,1)
   return s
end
