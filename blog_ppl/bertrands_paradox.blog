/*

  Bertrand's Box Paradox in BLOG.

  From https://towardsdatascience.com/five-paradoxes-with-probabilities-that-will-puzzle-you-2f71201d6ee8
  """
  3. Bertrand's Box Paradox

  If you are familiar with the Montey Hall problem, this paradox is quite 
  similar. In front of us there are three boxes:

    [image of three boxes]

  One box contains two silver coins, one box contains two gold coins and 
  one box contains a gold- and a silver coin. We do not know which coins 
  are in which box. Now, we pick a random box and blindly draw a coin from 
  our box. It’s a gold coin!

  Now, the question is:

    What’s the probability that the second coin in our box is also a gold coin?

  My naive (and wrong) answer when encountering the problem for the first 
  time was ½. I thought that because we drew a gold coin, our box is either 
  the one with the two gold coins or the one with the mixed coins. In the 
  first case, we would draw another gold coin and in the second case, we 
  wouldn’t. Therefore, I presumed the probability should be ½.

    The real probability is ⅔.

  The reason for that is that the first gold coin we drew could either be 
  the only gold coin in the mixed box, the first gold coin in the solely 
  golden box, or the second gold coin in the solely golden box. And in 
  two of these three possibilities, we will draw another gold coin.

  """

  $ blog -n 100000 bertrands_paradox.blog
"""
========  LW Trial Stats =========
Log of average likelihood weight (this trial): -0.6940575948612153
Average likelihood weight (this trial): 0.4995450000000459
Fraction of consistent worlds (this trial): 0.66658
Fraction of consistent worlds (running avg, all trials): 0.66658
======== Query Results =========
Number of samples: 100000
Distribution of values for (coin2 = gold)
	true	0.6656257194046072
	false	0.3343742805953142
Distribution of values for coin1
	gold	1.0
Distribution of values for coin2
	gold	0.6656257194046072
	silver	0.3343742805953142
Distribution of values for box
	box1	0.6656257194046072
	box2	0.3343742805953142
Distribution of values for (box = box1)
	true	0.6656257194046072
	false	0.3343742805953142
Distribution of values for (box = box2)
	false	0.6656257194046072
	true	0.3343742805953142
Distribution of values for (box = box3)
	false	1.0
======== Done ========
"""

  The answer to the 'paradox' is thus 2/3:
  """
  Distribution of values for (coin2 = gold)
	true	0.6656257194046072
	false	0.3343742805953142
  """

*/

type Box;
distinct Box box1, box2, box3;

type Coin;
distinct Coin gold, silver;

// Pick one box randomly.
random Box box ~ UniformChoice({b for Box b});

// The first coin we pick from the box is a gold coin.
random Coin coin1 ~
    case box in
      {
         box1 -> Categorical({gold -> 2, silver -> 0}),
         box2 -> Categorical({gold -> 1, silver -> 1}),
         box3 -> Categorical({gold -> 0, silver -> 2})
      };

// We pick another coin from the same box as the first coin,
// - a gold coin - so we have to count down the possible
// gold coins.
random Coin coin2 ~
    case box in
      {
         box1 -> Categorical({gold -> 1, silver -> 0}),
         box2 -> Categorical({gold -> 0, silver -> 1}),
         box3 -> Categorical({gold -> 0, silver -> 2})
      };


// We know that the first coin is a gold coin
obs coin1 = gold;

// Probability that the second coin in a gold coin.
query coin2 == gold;
query coin1;
query coin2;
query box;
query box == box1;
query box == box2;
query box == box3;
