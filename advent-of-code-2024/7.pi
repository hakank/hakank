/* 

  Advent of Code 2024 in Picat.

  Problem 7
  https://adventofcode.com/2024/day/7

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.

main => go.

go ?=>
  part1,
  part2.
go => true.

/*
  Hyperfine

  Benchmark 1: picat -g part1 7.pi
    Time (mean ± σ):      66.4 ms ±   7.5 ms    [User: 50.3 ms, System: 16.0 ms]
    Range (min … max):    47.6 ms …  82.3 ms    41 runs

*/
part1 => 
  Lines = [L.split(" :").map(to_int) : L in read_file_lines("7.txt")],
  println([N : [N|Ns] in Lines, eq(Ns,0,N)].sum).

eq([],S,S).
eq([A|L],S0,S) :-
 S0 <= S,
 (S1 = S0*A ; S1 = S0+A),
 eq(L,S1,S).


%
% Hyperfine
%
% Benchmark 1: picat -g part2 7.pi
%   Time (mean ± σ):      2.332 s ±  0.013 s    [User: 2.312 s, System: 0.019 s]
%   Range (min … max):    2.322 s …  2.359 s    10 runs
%
part2 =>
  Lines = [L.split(" :").map(to_int) : L in read_file_lines("7.txt")],
  println([N : [N|Ns] in Lines, (eq(Ns,0,N); eq2(Ns,0,N))].sum).

eq2([],S,S).
eq2([A|L],S0,S) :-
  S0 <= S, % Weeding out impossible paths
  (
    SA = S0*10**floor(log10(A)+1) + A, % Thanks to DestyNova 
    eq2(L,SA,S)
  ;
    (S1 = S0*A ; S1 = S0+A),
    eq2(L,S1,S)
  ).
