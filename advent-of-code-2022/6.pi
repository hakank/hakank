/* 

  Advent of Code 2022 - Day 6 in Picat.

  https://adventofcode.com/2022/day/6

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.
import cp. % for all_different/1

main => go.

% Logic programming
go =>
  % File = "6_test.txt",
  File = "6.txt",  
  Line = read_file_lines(File).first,
  member(Part,1..2),
  S = new_list(cond(Part==1,4,14)),
  ( (find(Line,S,From,To),all_different(S)) -> 
     println(To)
     ;
     true
  ),
  fail.


% Imperative variant
go2 =>
  % File = "6_test.txt",
  File = "6.txt",  
  Line = read_file_lines(File).first,
  member(Part,1..2),
  C1 = cond(Part==1,4,14)-1,
  Found = false,
  foreach(I in 1..Line.len-C1, break(Found==true), all_different(Line[I..I+C1]))
    println(I+C1),
    Found := true
  end,
  fail,
  nl.


