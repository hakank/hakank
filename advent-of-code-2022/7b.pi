/* 

  Advent of Code 2022 - Day 7 in Picat.

  https://adventofcode.com/2022/day/7

  Another take on this.

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.

main => go.


go =>
  % File = "7_test.txt",
  File = "7.txt",
  Lines = read_file_lines(File),

  Fs = new_map(),
  Dirs = [],
  check(Lines,Dirs,Fs),

  % Part 1
  TotalPart1 = sum([Size : _Dir=Size in Fs, Size <= 100000]),
  println(part1=TotalPart1),

  % Part 2
  TotalNeedToFree = 30000000 - (70000000 - Fs.get("//")),
  println(part2=[ Size : _Dir=Size in Fs, Size >= TotalNeedToFree ].min),
  
  nl.

check([],Dirs,Fs) => true.
check([Line|Lines],Dirs,Fs), append("$ cd ",Dir,Line) ?=>
  if Dir == ".." then
    Dirs := pop(Dirs,_CD)
  else
    Parents = Dirs[1..Dirs.len-1].join("/"),
    Dirs := push(Dirs,[Parents ++ "/" ++ Dir])
  end,
  check(Lines,Dirs,Fs).
check([Line|Lines],Dirs,Fs), Line == "$ ls" ?=>
  check(Lines,Dirs,Fs).
check([Line|Lines],Dirs,Fs), append("dir ",_Dir,Line) ?=>
  check(Lines,Dirs,Fs).
check([Line|Lines],Dirs,Fs), append(Size, " ",_Name,Line) =>
  SizeInt = Size.to_int,
  foreach(D in Dirs)
    Fs.put(D,Fs.get(D,0)+SizeInt)
  end,
  check(Lines,Dirs,Fs).

push(L,E) = E ++ L, list(E) => true. % If E is a list
push(L,E) = [E] ++ L. % E is an atom

pop(L,E) = Rest, not empty(L) =>
  L = [E|Rest].

empty([]).

