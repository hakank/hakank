/* 

  Advent of Code 2022 - Day 4 in Picat.

  https://adventofcode.com/2022/day/4

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.

main => go.

go =>
  % File = "4_test.txt",
  File = "4.txt",
  Lines = read_file_lines(File),

  C1 = 0, C2 = 0,
  foreach(Line in Lines)
    [A1,B1] = split(Line,","),
    A = A1.split("-").map(to_int),
    B = B1.split("-").map(to_int),
    if overlap_completely(A,B) then C1 := C1 + 1 end,  % Part 1
    if overlap_partly(A,B)     then C2 := C2 + 1 end % Part 2
  end,
  println(C1),
  println(C2),
  nl.

% Part 1
overlap_completely([LowerA,UpperA], [LowerB,UpperB]) :-  
    ( LowerA <= LowerB, UpperA >= UpperB ) ;
    ( LowerB <= LowerA, UpperB >= UpperA ).

% Part 2
overlap_partly([LowerA,UpperA], [LowerB,UpperB]) :-
    LowerA =< UpperB,
    UpperA >= LowerB.

