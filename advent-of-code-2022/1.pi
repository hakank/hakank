/* 

  Advent of Code 2022 - Day 1 in Picat.

  https://adventofcode.com/2022/day/1
  
  See https://github.com/hakank/hakank/tree/master/advent-of-code-2022
  

  Note: This uses my regex module https://github.com/hakank/picat_regex
  to simplify some text processing.

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import util.
import regex.

main => go.

go =>
  % File = "1_test.txt",
  File = "1.txt",
  Total = [split(Line,"\n").map(to_int).sum : Line in split_string(read_file_chars(File),"\n\n")],
  println(Total.max),
  println(Total.sort_down.take(3).sum).


% Using NFZ's split2/2 for splitting the string
go2 =>
  % File = "1_test.txt",
  File = "1.txt", 
  Total = [split(Line,"\n").map(to_int).sum : Line in split2(read_file_chars(File))],  
  println(Total.max),
  println(Total.sort_down.take(3).sum).


/*
  split_string(Str,Sep) = Lines

  Splits the string with a separator string Sep, e.g. "\n\n", 
  and returns the splitted lines Lines.

  Note: This uses my regex module: https://github.com/hakank/picat_regex
  
*/
split_string(Str,Sep) = regex_replace(Sep,"|",Str).split("|").


/*
  split2(String, Lines)
 
  Split the string String with "\n\n" as separator, 
  returns the splitted lines.

  (From Neng-Fa Zhou.)
  
*/
split2(Str) = Lines =>
  split2(Str,Lines).

% From NFZ
split2("\n\n",Tokens) => Tokens = [].
split2(S,Tokens), append(Token,"\n\n",Rest,S) =>
    Tokens = [Token|TokensR],
    split2(Rest,TokensR).
split2(S,Tokens) =>
    Tokens = [S].
