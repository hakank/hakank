/* 

  Advent of Code 2022 - Day 25 in Picat.

  https://adventofcode.com/2022/day/25

  This program was created by Hakan Kjellerstrand, hakank@gmail.com
  See also my Picat page: http://www.hakank.org/picat/

*/

import cp.

main => go.

% Part 1
go =>
  % File = "25_test.txt",
  File = "25.txt",    
  Lines = read_file_lines(File),
  Sum = sum([N : Line in Lines, Len=Line.len,
                 N = sum([SC*5**(Len-I) : {C,I} in zip(Line,1..Len), s(C,SC)])]),
  println(sum=Sum), 
  member(Len2,1..100),
  X = new_list(Len2),
  X :: -2..2,
  Sum #= sum([X[I]*5**(Len2-I) : I in 1..Len2]),
  println(x=X),
  println([T : S in X, s(T,S)]), % Convert to SNAFU
  nl.

s('2',2).
s('1',1).
s('0',0).
s('-',-1).
s('=',-2).
