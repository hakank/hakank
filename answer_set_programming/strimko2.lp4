% 
% Strimko in ASP.
% 
% From 
% 360: A New Twist on Latin Squares
% http://threesixty360.wordpress.com/2009/08/04/a-new-twist-on-latin-squares/
% """
% The idea is simple: each row and column of an nxn grid must contain 
% the number 1, 2, ... n exactly once (that is, the grid must form a 
% Latin square), and each "stream" (connected path in the grid) must 
% also contain the numbers 1, 2, ..., n exactly once.
% """
%
% For more information, see:
% * http://www.strimko.com/
% * http://www.strimko.com/rules.htm
% * http://www.strimko.com/about.htm
% * http://www.puzzlersparadise.com/Strimko.htm
% 
% I wrote about Strimko puzzles in
% 'Strimko - Latin squares puzzle with "streams"'
% http://www.hakank.org/constraint_programming_blog/2009/08/strimko_latin_squares_puzzle_w_1.html
%
%
% This was created by Hakan Kjellerstrand, hakank@gmail.com
% See also http://www.hakank.org/answer_set_programming/
%

% domain
number(1..n).

%
% Latin square:
%
% all rows and columns must have different values
1 { x(X,Y,N):number(N) } 1 :- number(X),number(Y).
1 { x(X,Y,N):number(X) } 1 :- number(N),number(Y).
1 { x(X,Y,N):number(Y) } 1 :- number(N),number(X). 

%
% place the placed hints
%
x(X,Y,N) :- placed(X,Y,N).

%
% streams: 
%
% for all the values the belong to the same stream 
% (i.e. with the same stream number) there must be 
% different values.
1 { x(X,Y,N) : streams(X,Y,M), number(X),number(Y)} 1 :- number(N),number(M).


#show x/3.