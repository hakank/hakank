% 
% Sudoku problem in ASP.
% 
% See http://en.wikipedia.org/wiki/Sudoku
% 
% This was inspired by the encoding in
% https://noppa.tkk.fi/noppa/kurssi/t-79.5105/esite
%
% This was created by Hakan Kjellerstrand, hakank@gmail.com
% See also http://www.hakank.org/answer_set_programming/
%

%
% Clues for the problem from
% http://www.tellmehowto.net/sudoku/veryhardsudoku.html
%
% _,_,6, _,_,_, _,9,_,
% _,_,_, 5,_,1, 7,_,_,
% 2,_,_, 9,_,_, 3,_,_,
%
% _,7,_, _,3,_, _,5,_,
% _,2,_, _,9,_, _,6,_,
% _,4,_, _,8,_, _,2,_,
%
% _,_,1, _,_,3, _,_,4,
% _,_,5, 2,_,7, _,_,_,
% _,3,_, _,_,_, 8,_,_
%
x(1, 3, 6).
x(1, 8, 9).
x(2, 4, 5).
x(2, 6, 1).
x(2, 7, 7).
x(3, 1, 2).
x(3, 4, 9).
x(3, 7, 3).
x(4, 2, 7).
x(4, 5, 3).
x(4, 8, 5).
x(5, 2, 2).
x(5, 5, 9).
x(5, 8, 6).
x(6, 2, 4).
x(6, 5, 8).
x(6, 8, 2).
x(7, 3, 1).
x(7, 6, 3).
x(7, 9, 4).
x(8, 3, 5).
x(8, 4, 2).
x(8, 6, 7).
x(9, 2, 3).
x(9, 7, 8).


%
% domains
%
val(1..9).
border(1;4;7).

% alldifferent boxes
1 { x(X,Y,N) : val(X),val(Y), 
    X1<=X, X<=X1+2, Y1<=Y, Y<=Y1+2 } 1 :- val(N), border(X1),border(Y1).

% alldifferent rows, columns, values
1 { x(X,Y,N) : val(N) } 1 :- val(X),val(Y).
1 { x(X,Y,N) : val(X) } 1 :- val(N),val(Y).
1 { x(X,Y,N) : val(Y) } 1 :- val(N),val(X). 

% alternative:
% :- 2 { x(X,Y,N) : val(N) }, val(X;Y).
% :- 2 { x(X,Y,N) : val(X) }, val(N;Y).
% :- 2 { x(X,Y,N) : val(Y) }, val(N;X). 
