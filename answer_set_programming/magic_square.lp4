% 
% Magic square in ASP.
%
% See http://en.wikipedia.org/wiki/Magic_square
%
% 
% This was created by Hakan Kjellerstrand, hakank@gmail.com
% See also http://www.hakank.org/answer_set_programming/
%

#const n = 3.               % the size
#const s = n*(n*n + 1) / 2. % the num

%
% domains
%
size(1..n).
val(1..n*n).

% unique index of x
1 { x(Row, Col, N) : val(N) } 1 :- size(Row),size(Col).

% alldifferent values of x
1 { x(Row, Col, N) : size(Row),size(Col) } 1 :- val(N).


% sum rows
:- not s #sum{ Val: x(Row, Col, Val), size(Col), val(Val) } s, size(Row).

% sum columns
:- not s #sum{Val: x(Row, Col, Val), size(Row), val(Val) } s, size(Col).

% Sum diagonals 1 and 2
:- not s #sum { Val:x(I, I, Val), size(I), val(Val) } s.
:- not s #sum { Val:x(I, n-I+1, Val), size(I), val(Val) } s.

#show x/3.