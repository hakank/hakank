// 
// Project Euler problem 14 in Frink
//
// Problem 14
//
// """
// The following iterative sequence is defined for the set of positive integers:
//
// n n/2 (n is even)
// n 3n + 1 (n is odd)
//
// Using the rule above and starting with 13, we generate the following 
// sequence:
// 13 40 20 10 5 16 8 4 2 1
//
// It can be seen that this sequence (starting at 13 and finishing at 1) 
// contains 
// 10 terms. Although it has not been proved yet (Collatz Problem), it is 
// thought that all starting numbers finish at 1.
//
// Which starting number, under one million, produces the longest chain?
//
// NOTE: Once the chain starts the terms are allowed to go above one million.")
// """
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//

// collatz[n] := n % 2 == 0 ? n \ 2 : 3*n + 1

collatzLen[n, cache] :=
{
  if cache.containsKey[n]
    return cache@n

  if n <= 1 
     return cache@n = 1

  if n % 2 == 0
    return cache@n = collatzLen[n \ 2, cache] + 1
  else
    return cache@n = collatzLen[3*n + 1, cache] + 1
}


longestSeq = 1
startingNum = 1
cache = new dict
// for j = 2 to 1000000-1
// This is faster
for j = new range[2,1000000-1]

{
  c = collatzLen[j, cache]
  if c > longestSeq 
  {
    longestSeq = c;
    startingNum = j;
    // println["\tj:$j longestSeq: $longestSeq startingNum:$startingNum"]
  }
}

println["$startingNum"]

