// 
// Dinesman's multiple-dwelling problem in Frink
// 
// From
// http://rosettacode.org/wiki/Dinesman%27s_multiple-dwelling_problem
// """
// The task is to solve Dinesman's multiple dwelling problem but in a way that most 
// naturally follows the problem statement given below. Solutions are allowed (but 
// not required) to parse and interpret the problem text, but should remain flexible 
// and should state what changes to the problem text are allowed. Flexibility and 
// ease of expression are valued.
// 
// Examples may be be split into "setup", "problem statement", and "output" sections 
// where the ease and naturalness of stating the problem and getting an answer, 
// as well as the ease and flexibility of modifying the problem are the primary concerns.
//
// Example output should be shown here, as well as any comments on the examples flexibility.
//
// The problem
//   Baker, Cooper, Fletcher, Miller, and Smith live on different floors of an 
//   apartment house that contains only five floors. Baker does not live on the 
//   top floor. Cooper does not live on the bottom floor. Fletcher does not live 
//   on either the top or the bottom floor. Miller lives on a higher floor than 
//   does Cooper. Smith does not live on a floor adjacent to Fletcher's. Fletcher 
//   does not live on a floor adjacent to Cooper's. Where does everyone live? 
// """
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my Frink page: http://www.hakank.org/frink/
//



//
// a is adjacent to b
//
adjacent[a,b] := abs[a-b] == 1


//
// The constraints
//
// floors: 1: bottom .. 5: top floor
//
// Baker not top floor
// Cooper not bottom floor  
// Fletcher not botton nor top floor
// Miller higher floor than Cooper
// Smith and Fletcher not adjacent 
// Fletcher and Cooper not adjacent

constraints[p] :=
{
  [b,c,f,m,s] = p
  return b != 5 && c != 1 && f != 1 && f != 5 && m > c && !adjacent[s, f] && !adjacent[f, c]
}


// Using permutations
dinesman1[] :=
{ 
  println["\ndinesman1:"]
  for [b,c,f,m,s] = array[1 to 5].permute[]
     if constraints[[b,c,f,m,s]]
        println["Baker: $b Cooper: $c Fletcher: $f Miller: $m Smith $s"]
}

alldifferent[s] := length[toSet[s]] == length[s]


// Using multifor and alldifferent
dinesman2[] :=
{
   println["\ndinesman2:"]
   n = 5
   multifor p2 = makeArray[[n], array[1 to n]]
   {
      if alldifferent[p2] && constraints[p2]
      {
         [b,c,f,m,s] = p2
         println["Baker: $b Cooper: $c Fletcher: $f Miller: $m Smith $s"]
      }
   }
}


dinesman1[]
dinesman2[]
