//
// Euler problem 27 in Frink
// """
// Euler published the remarkable quadratic formula:
//
// n^2 + n + 41
//
// It turns out that the formula will produce 40 primes for the consecutive values 
// n = 0 to 39. However, when n = 40, 402 + 40 + 41 = 40(40 + 1) + 41 is divisible by 
// 41, and certainly when n = 41, 41^2 + 41 + 41 is clearly divisible by 41.
//
// Using computers, the incredible formula  n^2 − 79n + 1601 was discovered, which 
// produces 80 primes for the consecutive values n = 0 to 79. The product of the 
// coefficients, −79 and 1601, is −126479.
//
// Considering quadratics of the form:
//
//     n^2 + an + b, where |a| < 1000 and |b| < 1000
//
//     where |n| is the modulus/absolute value of n
//     e.g. |11| = 11 and |−4| = 4
//
// Find the product of the coefficients, a and b, for the quadratic 
// expression that produces the maximum number of primes for consecutive 
// values of n, starting with n = 0.
// 
// """ 
//
// This Frink program was created by Hakan Kjellerstrand (hakank@bonetmail.com).
// See also my Frink page: http://www.hakank.org/frink/
//

p[a,b] := 
{  
  n = 0
  pp = n^2 + a*n + b
  while pp > 1 and isPrime[pp]
  {
    n = n+1
    pp = n^2 + a*n + b
  }
  return n
}


t = 999
bestLen = besta = bestb = 0
multifor [a, b] = [-t to t, -t to t]
{
  llen = p[a,b]
  if llen > bestLen
  {
    bestLen = llen
    besta = a
    bestb = b
  }
}

// println["besta: $besta bestb: $bestb bestLen: $bestLen answer: " + besta*bestb]
println[besta*bestb]

