$
$ SEND + MOST = MONEY in Essence'.
$
$ Alphametic problem were we maximize MONEY.
$   
$ Problem from the lecture notes:
$ http://www.ict.kth.se/courses/ID2204/notes/L01.pdf
$
$ Compare with the following models:
$ * Comet: http://www.hakank.org/comet/send_most_money.co
$ * Comet: http://www.hakank.org/comet/send_most_money2.co
$ * ECLiPSE: http://www.hakank.org/eclipse/send_most_money.ecl
$ * MiniZinc: http://www.hakank.org/minizinc/send_most_money.mzn
$ * Gecode/R: http://www.hakank.org/gecode_r/send_most_money2.rb
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
$
ESSENCE' 1.0

find s,e,n,d,m,o,t,y : int(0..9)
find money : int(0..100000)

maximising money

such that
          alldiff([s,e,n,d,m,o,t,y]),

          1000*s + 100*e + 10*n + d 
        + 1000*m + 100*o + 10*s + t = money,
          money = 10000*m + 1000*o + 100*n + 10*e + y,

          s > 0,
          m > 0
