$
$ Least diff problem in Essence'.
$
$ The model solves the following problem:
$  
$  What is the smallest difference between two numbers X - Y
$  if you must use all the digits (0..9) exactly once, i.e.
$  minimize the difference ABCDE - FGHIJ.
$
$ Compare with the following models:
$  * MiniZinc: http://www.hakank.org/minizinc/least_diff.mzn
$  * Choco   : http://www.hakank.org/choco/LeastDiff2.java
$  * JaCoP   : http://www.hakank.org/JaCoP/LeastDiff.java
$  * Gecode/R: http://www.hakank.org/gecode_r/least_diff.rb
$  * Comet   : http://www.hakank.org/comet/least_diff.co
$  * Gecode  : http://www.hakank.org/gecode/least_diff.cpp
$  * ECLiPSe : http://www.hakank.org/eclipse/least_diff.ecl
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Tailor/Essence' page: http://www.hakank.org/minion_tailor
$ 
ESSENCE' 1.0
letting LD be domain int(0..9)
letting RES be domain int(0..200000)

find a,b,c,d,e,f,g,h,i,j : LD,
     res : RES

minimising res

such that
          alldiff([a,b,c,d,e,f,g,h,i,j]),
          res = (10000*a +1000*b +100*c +10*d + e)  -
          (10000*f +1000*g +100*h +10*i + j),
          a > 0,
          f > 0          
$          ,   res <= 250
          

