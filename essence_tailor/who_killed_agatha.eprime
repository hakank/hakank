$
$ Who killed agatha? (The Dreadsbury Mansion Murder Mystery) in Essence'.
$
$ This is a standard benchmark for theorem proving.
$
$ http://www.lsv.ens-cachan.fr/~goubault/H1.dist/H1.1/Doc/h1003.html
$
$ """ 
$ Someone in Dreadsbury Mansion killed Aunt Agatha. 
$ Agatha, the butler, and Charles live in Dreadsbury Mansion, and 
$ are the only ones to live there. A killer always hates, and is no 
$ richer than his victim. Charles hates noone that Agatha hates. Agatha 
$ hates everybody except the butler. The butler hates everyone not richer 
$ than Aunt Agatha. The butler hates everyone whom Agatha hates. 
$ Noone hates everyone. Who killed Agatha? 
$ """
$
$ Originally from F. J. Pelletier: 
$ Seventy-five problems for testing automatic theorem provers. 
$ Journal of Automated Reasoning, 2: 191Ã¢â‚¬â€œ216, 1986.
$
$ Compare with the following models: 
$ * Choco: http://www.hakank.org/choco/WhoKilledAgatha.java
$ * Choco: http://www.hakank.org/choco/WhoKilledAgatha_element.java
$ * Comet: http://www.hakank.org/comet/who_killed_agatha.co
$ * ECLiPSE: http://www.hakank.org/eclipse/who_killed_agatha.ecl
$ * Gecode: http://www.hakank.org/gecode/who_killed_agatha.cpp
$ * JaCoP: http://www.hakank.org/JaCoP/WhoKilledAgatha.java
$ * JaCoP: http://www.hakank.org/JaCoP/WhoKilledAgatha_element.java
$ * MiniZinc: http://www.hakank.org/minizinc/who_killed_agatha.mzn
$
$
$ Note: There are 8 solutions, all stating that Agatha killed herself
$
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
$
ESSENCE' 1.0

letting n be 3
letting Range be domain int(1..n)
letting agatha  be 1
letting butler  be 2
letting charles be 3

find the_killer : int(1..n)
find the_victim : int(1..n)
find hates : matrix indexed by [int(1..n),int(1..n)] of bool
find richer : matrix indexed by [int(1..n),int(1..n)] of bool

such that

   $ Agatha, the butler, and Charles live in Dreadsbury Mansion, and 
   $ are the only ones to live there. 

   $ A killer always hates, and is no richer than his victim. 
   hates[the_killer, the_victim] = 1,

   richer[the_killer, the_victim] = 0,

   $ define the concept of richer: no one is richer than him-/herself
   forall i : Range .
      richer[i,i] = 0,

   $ (contd...) if i is richer than j then j is not richer than 
   forall i,j : Range .
      (i != j) => (richer[i,j] = 1) <=> (richer[j,i] = 0),

   $ Charles hates noone that Agatha hates. 
   forall i : Range . 
      (hates[agatha, i] = 1) => (hates[charles, i] = 0),

   $ Agatha hates everybody except the butler. 
   hates[agatha, charles] = 1,
   hates[agatha, agatha] = 1,
   hates[agatha, butler] = 0,

   $ The butler hates everyone not richer than Aunt Agatha. 
   forall i : Range .
      (richer[i, agatha] = 0) => (hates[butler, i] = 1), 

   $ The butler hates everyone whom Agatha hates. 
   forall i : Range .
      (hates[agatha, i] = 1) => (hates[butler, i] = 1),

   $ Noone hates everyone. 
   forall i : Range .
     (sum j : Range . hates[i,j]) <= 2,
     
   $ Who killed Agatha? 
   the_victim = agatha
