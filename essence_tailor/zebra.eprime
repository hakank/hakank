$
$ Zebra puzzle in Essence'.
$
$ From zebra.mzn in the MiniZinc distribution.
$
$ WHO OWNS THE ZEBRA?
$ 
$ This is a puzzle which has been circulating the net. There are a couple
$ different versions out there which try to be a little more politically
$ correct but are essentially the same problem.    
$ 	1. There are five houses, each of a different color and inhabited by
$	   men of different nationalities, with different pets, drinks,
$	   and cigarettes.
$ 	2. The Englishman lives in the red house.
$ 	3. The Spaniard owns the dog.
$ 	4. Coffee is drunk in the green house.
$ 	5. The Ukrainian drinks tea.
$ 	6. The green house is immediately to the right of the ivory house.
$ 	7. The Old Gold smoker owns snails.
$ 	8. Kools are smoked in the yellow house.
$ 	9. Milk is drunk in the middle house.
$ 	10. The Norwegian lives in the first house on the left.
$ 	11. The man who smokes Chesterfields lives in the house next to the
$	    man with the fox.
$ 	12. Kools are smoked in the house next to the house where the horse is
$	    kept.
$ 	13. The Lucky Strike smoker drinks orange juice.
$ 	14. The Japanese smoke Parliaments.
$ 	15. The Norwegian lives next to the blue house.
$ NOW, who drinks water? And who owns the zebra?

$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
$
ESSENCE' 1.0

$ Note: I tried with 1..5 (using Minion) but got some weird errors of 0'th index.
$       Using 0..4 works, though.
$
$ The solution is
$ Sol: 3 0 1 4 2  $ animal
$ Sol: 2 4 3 0 1  $ colour
$ Sol: 4 1 2 3 0  $ drink
$ Sol: 2 3 1 0 4  $ nation
$ Sol: 2 0 1 3 4  $ smoke

letting English be 0
letting Spanish be 1
letting Ukrainian be 2
letting Norwegian be 3
letting Japanese be 4

letting Red be 0
letting Green be 1
letting Ivory be 2
letting Yellow be 3
letting Blue be 4

letting Dog be 0
letting Fox be 1
letting Horse be 2
letting Zebra be 3
letting Snails be 4

letting Coffee be 0
letting Tea be 1
letting Milk be 2
letting OrangeJuice be 3
letting Water be 4

letting OldGold be 0
letting Kools be 1
letting Chesterfields be 2
letting LuckyStrike be 3
letting Parliaments be 4

find animal, colour, drink, nation, smoke : matrix indexed by [int(0..4)] of int(0..4)

such that

	alldiff(animal),                          $ 1
	alldiff(colour),
	alldiff(drink),
	alldiff(nation),
	alldiff(smoke),

	nation[English] = colour[Red],            $ 2 
 	nation[Spanish] = animal[Dog],            $ 3
 	drink[Coffee] = colour[Green],            $ 4
        nation[Ukrainian] = drink[Tea],           $ 5
        colour[Green] = colour[Ivory] + 1,        $ 6
        smoke[OldGold] = animal[Snails],          $ 7
        smoke[Kools] = colour[Yellow],            $ 8
        drink[Milk] = 2,                          $ 9
        nation[Norwegian] = 0,                    $ 10
        |smoke[Chesterfields] - animal[Fox]| = 1, $ 11
        |smoke[Kools] - animal[Horse]| = 1,       $ 12
        smoke[LuckyStrike] = drink[OrangeJuice],  $ 13
        nation[Japanese] = smoke[Parliaments],    $ 14
        |nation[Norwegian] - colour[Blue]| = 1    $ 15
