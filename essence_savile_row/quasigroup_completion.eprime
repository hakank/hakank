$
$ Quasigroup completion problem in Essence'.
$ 
$ See 
$ Carla P. Gomes and David Shmoys:
$ "Completing Quasigroups or Latin Squares: Structured Graph Coloring Problem"
$
$ 
$ See also
$ Ivars Peterson "Completing Latin Squares"
$ http://www.maa.org/mathland/mathtrek_5_8_00.html
$ """
$ Using only the numbers 1, 2, 3, and 4, arrange four sets of these numbers into 
$ a four-by-four array so that no column or row contains the same two numbers. 
$ The result is known as a Latin square.
$ ...
$ The so-called quasigroup completion problem concerns a table that is correctly 
$ but only partially filled in. The question is whether the remaining blanks in 
$ the table can be filled in to obtain a complete Latin square (or a proper 
$ quasigroup multiplication table).
$ """
$
$ Compare with the following models: 
$ * Choco: http://www.hakank.org"/choco/QuasigroupCompletion.java
$ * Comet: http://www.hakank.org"/comet/quasigroup_completion.co
$ * ECLiPSE: http://www.hakank.org"/eclipse/quasigroup_completion.ecl
$ * Gecode: http://www.hakank.org"/gecode/quasigroup_completion.cpp
$ * Gecode/R: http://www.hakank.org"/gecode_r/quasigroup_completion.rb
$ * JaCoP: http://www.hakank.org"/JaCoP/QuasigroupCompletion.java
$ * MiniZinc: http://www.hakank.org"/minizinc/quasigroup_completion.mzn
$
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
language ESSENCE' 1.0

given n: int $ size
given mat : matrix indexed by [int(1..n),int(1..n)] of int(0..n)

find x : matrix indexed by [int(1..n),int(1..n)] of int(1..n)

such that

    $ copy matrix
    forall i,j : int(1..n) . 
      (mat[i,j] > 0) => (x[i,j] = mat[i,j]),


    $ make it a Latin Square
    forall i : int(1..n) .
        allDiff(x[i,..]),

    forall j : int(1..n) .
        allDiff(x[..,j])
