$
$ Set covering problem in Essence'.
$
$ Example 9.1-2, page 354ff, from Taha "Operations Research - An Introduction"
$ Minimize the number of security telephones in street corners on a campus.
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
$
language ESSENCE' 1.0

letting n be 8
letting num_streets be 11
letting corner: matrix indexed by [int(1..num_streets), int(1..2)] of int(1..n) be 
  [[1,2],
   [2,3],
   [4,5],
   [7,8],
   [6,7],
   [2,6],
   [1,6],
   [4,7],
   [2,4],
   [5,8],
   [3,5]]

find x: matrix indexed by [int(1..n)] of int(0..1)
find z: int(0..1000)

minimising z

such that
   forall i: int(1..num_streets) .
      (sum j: int(1..n) . x[corner[i,1]] + x[corner[i,2]] ) >= 1
      $ (sum j: int(1..n) . (sum k: int(1..2) . x[corner[i,k]] )) >= 1
   ,
   z = sum j: int(1..n) . x[j]

   
