$
$ Stable Marriage in Essence'
$
$ Translation of the OPL version from
$ Pascal Van Hentenryck "The OPL Optimization Programming Language"
$ E.g.
$ http://www.comp.rgu.ac.uk/staff/ha/ZCSP/additional_problems/stable_marriage/stable_marriage.pdf
$
$
$ Essence' model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence' page: http://www.hakank.org/essence/
$

language ESSENCE' 1.0


given n: int $ number of women and men
letting people be domain int(1..n)

given rankWomen: matrix indexed by [people, people] of people
given rankMen: matrix indexed by [people, people] of people

$ decision variables
find wife: matrix indexed by [people] of people
find husband: matrix indexed by [people] of people

such that
   forall m: people . husband[wife[m]] = m,
   forall w: people . wife[husband[w]] = w,
   forall m,o: people . 
       (rankMen[m,o] < rankMen[m, wife[m]]) ->
           rankWomen[o,husband[o]] < rankWomen[o,m]
   ,
   forall w,o : people .
       (rankWomen[w,o] < rankWomen[w,husband[w]]) ->
            rankMen[o,wife[o]] < rankMen[o,w]
