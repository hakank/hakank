$ 
$ Jobs Puzzle in Essence'
$
$ (This is a standard problem in Automatic Reasoning.)
$
$
$ From http://www-unix.mcs.anl.gov/~wos/mathproblems/jobs.html
$ """
$ Jobs Puzzle
$ 
$ There are four people:  Roberta, Thelma, Steve, and Pete.
$  Among them, they hold eight different jobs.
$  Each holds exactly two jobs.
$  The jobs are chef, guard, nurse, clerk, police officer (gender not implied),
$  teacher, actor, and boxer.
$  The job of nurse is held by a male.
$  The husband of the chef is the clerk.
$  Roberta is not a boxer.
$  Pete has no education past the ninth grade.
$  Roberta, the chef, and the police officer went golfing together.
$
$  Question:  Who holds which jobs?
$
$
$ The answer:
$ Chef       Thelma
$ Guard      Roberta
$ Nurse      Steve
$ Clerk      Pete
$ Police     Steve
$ Teacher    Roberta
$ Actor      Pete
$ Boxer      Thelma

$
$ This Essence' model was created by Hakan Kjellerstrand, hakank@bonetmail.com .
$ See also my Tailor/Essence' page: http://www.hakank.org/tailor/ .
$
language ESSENCE' 1.0

letting Roberta be 1
letting Thelma be 2
letting Steve be 3
letting Pete be 4

letting n be 4
letting job be domain int(1..n)

find chef: job
find guard: job
find nurse: job
find clerk: job
find police_officer: job
find teacher: job
find actor: job
find boxer: job

find jobs: matrix indexed by [int(1..8)] of job

such that
   
    $ This type of array assignments would be nice to have
    $ jobs = [chef, guard, nurse, clerk, police_officer, teacher, actor, boxer],

    jobs[1] = chef, jobs[2] = guard, jobs[3] = nurse, jobs[4] = clerk,    
    jobs[5] = police_officer, jobs[6] = teacher, jobs[7] = actor, jobs[8] = boxer,    

    $  Each holds exactly two jobs.
    gcc(jobs,[1,2,3,4], [2,2,2,2]),

    $  The job of nurse is held by a male.
    (nurse = Steve \/ nurse = Pete),

    $  The husband of the chef is the clerk.
    (clerk = Steve \/ clerk = Pete),
    (chef = Roberta \/ chef = Thelma),
    chef != clerk,

    $  Roberta is not a boxer.
    Roberta != boxer,

    $  Pete has no education past the ninth grade.
    Pete != teacher,
    Pete != police_officer,
    Pete != nurse,

    $ Roberta, [and] the chef, and the police officer went golfing together.
    Roberta != chef,
    chef    != police_officer,
    Roberta != police_officer,

     $ From the name of the job
    (actor = Steve \/ actor = Pete)
