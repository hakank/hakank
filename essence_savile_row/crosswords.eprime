$
$ Cross word in Essence'.
$
$ This is a standard example for constraint logic programming. See e.g.
$
$ http://www.cis.temple.edu/~ingargio/cis587/readings/constraints.html
$ """
$ We are to complete the puzzle
$
$      1   2   3   4   5
$    +---+---+---+---+---+       Given the list of words:
$  1 | 1 |   | 2 |   | 3 |             AFT     LASER
$    +---+---+---+---+---+             ALE     LEE
$  2 | # | # |   | # |   |             EEL     LINE
$    +---+---+---+---+---+             HEEL    SAILS
$  3 | # | 4 |   | 5 |   |             HIKE    SHEET
$    +---+---+---+---+---+             HOSES   STEER
$  4 | 6 | # | 7 |   |   |             KEEL    TIE
$    +---+---+---+---+---+             KNOT
$  5 | 8 |   |   |   |   |
$    +---+---+---+---+---+       
$  6 |   | # | # |   | # |       The numbers 1,2,3,4,5,6,7,8 in the crossword
$    +---+---+---+---+---+       puzzle correspond to the words 
$                                                  that will start at those locations.
$ """
$
$ The model was inspired by Sebastian Brand's Array Constraint cross word example
$ http://www.cs.mu.oz.au/~sbrand/project/ac/
$ http://www.cs.mu.oz.au/~sbrand/project/ac/examples.pl
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
$
language ESSENCE' 1.0

$  The result:
$
$   E1 =  1  HOSES
$   E2 =  3  SAILS
$   E3 =  5  STEER
$   E4 =  7  HIKE
$   E5 =  8  KEEL
$   E6 = 12  ALE
$   E7 = 14  LEE
$   E8 =  2  LASER

letting A = [
   [8,15,19,5,19], $ hoses
   [12,1,19,5,18], $ laser
   [19,1,9,12,19], $ sails
   [19,8,5,5,20], $ sheet
   [19,20,5,5,18], $ steer
   [8,5,5,12,0], $ heel
   [8,9,11,5,0], $ hike
   [11,5,5,12,0], $ keel
   [11,14,15,20,0], $ knot
   [12,9,14,5,0], $ line
   [1,6,20,0,0], $ aft
   [1,12,5,0,0], $ ale
   [5,5,12,0,0], $ eel
   [12,5,5,0,0], $ lee
   [20,9,5,0,0] $ tie
   ]

$ The domains (possible positions in the cross word)
find E1 : int(1..5)
find E2 : int(1..5)
find E3 : int(1..5)
find E4 : int(6..10)
find E5 : int(6..10)
find E6 : int(11..15)
find E7 : int(11..15)
find E8 : int(1..5)

such that
  $ The overlapping positions in the cross word, i.e.
  $ where the letters is the same
   A[E1, 3] = A[E2, 1],  $  s
   A[E1, 5] = A[E3, 1],  $  s

   A[E4, 2] = A[E2, 3],  $  i
   A[E4, 3] = A[E5, 1],  $  k
   A[E4, 4] = A[E3, 3],  $  e

   A[E7, 1] = A[E2, 4],  $  l
   A[E7, 2] = A[E5, 2],  $  e
   A[E7, 3] = A[E3, 4],  $  e

   A[E8, 1] = A[E6, 2],  $  l
   A[E8, 3] = A[E2, 5],  $  s
   A[E8, 4] = A[E5, 3],  $  e
   A[E8, 5] = A[E3, 5]   $  r

