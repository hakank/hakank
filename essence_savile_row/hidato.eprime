$
$ Hidato puzzle in Essence'.
$  
$ http://www.shockwave.com/gamelanding/hidato.jsp
$ http://www.hidato.com/
$
$ """
$ Puzzles start semi-filled with numbered tiles.
$ The first and last numbers are circled.
$ Connect the numbers together to win. Consecutive
$ number must touch horizontally, vertically, or
$ diagonally.
$ """
$
$ Compare with the following models:
$ * MiniZinc: http://www.hakank.org/minizinc/hidato.mzn
$ * Comet   : http://www.hakank.org/comet/hidato.co
$ * Gecode  : http://www.hakank.org/gecode/hidato.cpp
$ * ECLiPSe : http://www.hakank.org/eclipse/hidato.ecl
$
$ Model created by Hakan Kjellerstrand, hakank@bonetmail.com
$ See also my Essence'/Tailor page: http://www.hakank.org/minion_tailor
$
language ESSENCE' 1.0

given n : int(1..100)
given puzzle : matrix indexed by [int(1..n), int(1..n)] of int(0..n*n)


$ solution:
$  6 7 9
$  5 2 8
$  1 4 3
$ letting n be 3
$ letting puzzle : matrix indexed by [int(1..n), int(1..n)] of int(0..n*n) be
$     [ 
$        [6,0,9],
$        [0,2,8],
$        [1,0,0]
$     ]


letting AB be domain int(-1,0,1)

find x : matrix indexed by [int(1..n),int(1..n)] of int(1..n*n)


such that

   $ note: flatten is needed for a matrix to be used with allDiff
   allDiff(flatten(x)),
   
   $ place the fixed tiles
   forall i,j : int(1..n) . (
       (puzzle[i,j] > 0) -> 
          (x[i,j] = puzzle[i,j])
   ),

   $ go through the numbers 1..n**2-1 and get
   $ their positions in x
   forall k : int(1..n*n-1) . (
     exists i,j : int(1..n) . (
       (k = x[i, j]) $ fix this k
       /\
       (
       exists a,b : AB . (
         $ get the valid values in the matrix
           ( i+a >= 1) /\ ( j+b >=  1) /\
            ( i+a <= n) /\ ( j+b <= n)
            /\ (!( (a = 0) /\ (b = 0))) $ must be a move
            /\ 
           (k + 1 = x[i+a, j+b]) $ find the next k
        )
        )
      )
    )


