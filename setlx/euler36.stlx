// 
// Project Euler problem 36 in SetlX
//
// Problem 36
// """
// The decimal number, 585 = 1001001001_(2) (binary), is palindromic 
// in both bases.
// 
// Find the sum of all numbers, less than one million, which are palindromic 
// in base 10 and base 2.
//
// (Please note that the palindromic number, in either base, may not include leading zeros.)
// """
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

problem36 := procedure() {
   res := 0;
   for (n in [1..999999]) {         
       if (is_palindromic(n)) {
          bin := dec_to_base(n, 2);
          if (bin == reverse(bin)) {
            res += n;
          }
       }
   }

   print(res);

};

is_palindromic := procedure(a) {
  return str(a) == reverse(str(a));
};

//
// convert a decimal number to base base
//
dec_to_base := procedure(n, base) {
  res := [];
  while (n > 0) {
    r := n % base;
    n := n \ base;
    res += [r];     
  }  
  return reverse(res);
};

problem36();