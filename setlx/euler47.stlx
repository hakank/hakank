// 
// Project Euler problem 47 in SETL
//
// """  
// The first two consecutive numbers to have two distinct prime factors are:
//
// 14 = 2 x 7
// 15 = 3 x 5
//
// The first three consecutive numbers to have three distinct 
// prime factors are:
//
// 644 = 2^2 x 7 x 23
// 645 = 3 x 5 x 43
// 646 = 2 x 17 x 19.
//
// Find the first four consecutive integers to have four distinct primes 
// factors. What is the first of these numbers?
//
// """
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

problem47 := procedure() {

    maxn := 1000000;
    f := [0 : i in [1..maxn]];
    for (i in [2..maxn-1]) {
        if (f[i] == 0) {
            for (j in [2*i,2*i+i..maxn-1]) {
                f[j] += 1;
            }
        }
    }
    
    goal := [4,4,4,4];
    print("goal: ", goal);
    for (i in [2..maxn-3]) {
        if (f[i..i+3] == goal) {
            print(i);
            break;
        }
    }

};


t1 := now();
problem47();
t2 := now();
print("It took ", t2-t1, "ms");
