// 
// Project Euler problem 3 in SetlX
//
// Problem 3
// """
// The prime factors of 13195 are 5, 7, 13 and 29.
// What is the largest prime factor of the number 600851475143 ?
// """
//
// This SETL program was created by Hakan Kjellerstrand (hakank@bonetmail.com)
// Also see my SETL page: http://www.hakank.org/setl/
//

even := procedure(n) {
   return n % 2 == 0;
};

prime_factors := procedure(n) {

   facts := []; 
   while (even(n)) { 
     facts += [2]; 
     n /= 2;
   }

   while (exists (k in [3,5..round(sqrt(n))] | round(n) % k == 0)) {
      facts += [k]; 
      n /= k;
   }
   facts += [n];

   return facts;
};

// Elegant but much slower
factors2 := procedure(n) {
  r := [f : f in [2..round(sqrt(n))] | n % f == 0 && (forall (m in [2..round(sqrt(f))] | f % m > 0)) ];
  return(r);
};

problem3 := procedure() {
  print("Problem 3: ");
  t1 := now();
  n := 600851475143;
  f := round(sqrt(n));
  fact := prime_factors(n);
  print(fact[#fact]);
  t2 := now();
  print("It took ", t2-t1, " ms");
};

problem3b := procedure() {

  print("Problem 3b:");
  t1 := now();
  n := 600851475143;
  fact := factors2(n);
  print(fact[#fact]);
  t2 := now();
  print("It took ", t2-t1, " ms");
};


problem3();
// problem3b();
