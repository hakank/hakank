/*
  Bertrand's Box Paradox in PSI.

  From https://towardsdatascience.com/five-paradoxes-with-probabilities-that-will-puzzle-you-2f71201d6ee8
  """
  3. Bertrand's Box Paradox

  If you are familiar with the Montey Hall problem, this paradox is quite 
  similar. In front of us there are three boxes:

    [image of three boxes]

  One box contains two silver coins, one box contains two gold coins and 
  one box contains a gold- and a silver coin. We do not know which coins 
  are in which box. Now, we pick a random box and blindly draw a coin from 
  our box. It’s a gold coin!

  Now, the question is:

    What’s the probability that the second coin in our box is also a gold coin?

  My naive (and wrong) answer when encountering the problem for the first 
  time was ½. I thought that because we drew a gold coin, our box is either 
  the one with the two gold coins or the one with the mixed coins. In the 
  first case, we would draw another gold coin and in the second case, we 
  wouldn’t. Therefore, I presumed the probability should be ½.

    The real probability is ⅔.

  The reason for that is that the first gold coin we drew could either be 
  the only gold coin in the mixed box, the first gold coin in the solely 
  golden box, or the second gold coin in the solely golden box. And in 
  two of these three possibilities, we will draw another gold coin.

  """

  Here we define the boxes as:
  - box1: 2 gold coins
  - box2: 1 gold and 1 silver coin
  - box3: 2 silver coins


  cf ~/blog/bertrands_paradox.blog
  

*/

// E[r1_,r2_,r3_,r4_,r5_] = (1,2/3,2/3,1/3,0)
// (1,0.666666666666667,0.666666666666667,0.333333333333333,0)
// r1: Coin1 is gold: 1
// r2: Coin2 is gold: 2/3
// r3: Box is box1: 2/3
// r4: Box is box2: 1/3
// r5: Box is box3: 0
def main() {

  box1 := 0;
  box2 := 1;
  box3 := 2;
  boxes := [box1,box2,box3];

  // We draw a box randomly
  box := boxes[categorical([1/3,1/3,1/3])];

  gold := 0;
  silver := 1;
  coins := [gold,silver];

  // We then draw a coin from that box: It's a gold coin.
  coin1 := 0;
  if box = box1 {
    coin1 = coins[categorical([2/2, 0/2])];
  } else if box = box2 {
    coin1 = coins[categorical([1/2, 1/2])];
  } else {
    coin1 = coins[categorical([0/2, 2/2])];  
  }

  // What is the probability that the second
  // coin from the selected box is also a gold coin?
  coin2 := 0;
  if box = box1 {
    coin2 = coins[categorical([1/1, 0/1])];
  } else if box = box2 {
    coin2 = coins[categorical([0/1, 1/1])];
  } else {
    coin2 = coins[categorical([0/1, 1/1])];  
  }

  observe(coin1 = gold);

  return(coin1 = gold,
         coin2 = gold,
         box = box1,
         box = box2,
         box = box3,
         );

}
